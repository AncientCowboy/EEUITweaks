menu
{
	name 'CHARGEN_RACE'
	align center center
	modal
	ignoreesc
	onOpen
	"
		currentChargenRace = nil
	"
	label
	{
		area 0 0 864 710
		mosaic LUICUSTB
	}
	label
	{
		area 82 12 700 40
		text "RACE_TITLE"
		text style "title"
	}
	list
	{
		column
		{
			width 100
			label
			{
				enabled "rowNumber ~= currentChargenRace and rowNumber ~= hoverChargenRace"
				area 8 0 340 44
				text lua "Infinity_FetchString(chargen.races[rowNumber].name)"
				text style "list"
				text align center center
				text color D
			}
			label
			{
				enabled "rowNumber == currentChargenRace and rowNumber ~= hoverChargenRace"
				area 8 0 340 44
				text lua "Infinity_FetchString(chargen.races[rowNumber].name)"
				text style "list"
				text align center center
				text color S
			}
			label
			{
				enabled "rowNumber == hoverChargenRace"
				area 8 0 340 44
				text lua "Infinity_FetchString(chargen.races[rowNumber].name)"
				text style "list"
				text align center center
				text color H
			}
		}
		action
		"
			if chargen.races[currentChargenRace] then
				createCharScreen:OnRaceSelectButtonClick(chargen.races[currentChargenRace].id)
			end
		"
		actionEnter	"hoverChargenRace = mouseoverRow"
		actionExit	"hoverChargenRace = 0"
		rowheight 44
		hidehighlight
		area 42 192 340 414
		table "chargen.races"
		var currentChargenRace
		scrollbar 'LUISCRC'
	}
	text
	{
		area 436 128 382 478
		text lua "raceOrGeneralHelp()"
		text style "normal"
		scrollbar 'LUISCRC'
	}
	button
	{
		on return
		bam LUIOSTUL
		area 210 640 234 44
		text style "button"
		text "DONE_BUTTON"
		clickable lua "createCharScreen:IsDoneButtonClickable() and currentChargenRace"
		action
		"
			Infinity_PopMenu()
			createCharScreen:OnDoneButtonClick()
		"
	}
	button
	{
		on escape
		bam LUIOSTUR
		area 464 640 234 44
		text style "button"
		text "BACK_BUTTON"
		action
		"
			Infinity_PopMenu()
			createCharScreen:OnCancelButtonClick()
		"
	}
}
