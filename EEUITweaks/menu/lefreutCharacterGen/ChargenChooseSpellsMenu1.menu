menu
{
	name 'CHARGEN_CHOOSE_SPELLS'
	modal
	align center center
	ignoreesc
	onOpen
	"
		table.sort(chargen.choose_spell, sortBySpellName)
		currentChargenChooseMageSpell = nil
		for idx, spell in pairs(chargen.choose_spell) do
			if spell.enabled then
				createCharScreen:OnLearnMageSpellButtonClick(idx)
			end
		end
		needSpecialist = false
	"
	label
	{
		area 0 0 864 710
		mosaic LUICUSTB
	}
	label
	{
		area 82 12 700 40
		text lua "t(chargen.title) .. ': ' .. t('LEVEL_LABEL') .. ' ' .. chargen.currentSpellLevelChoice"
		text style "title"
	}
	list
	{
		column
		{
			width 16
			label
			{
				bam LUIBTBUT
				area 0 0 52 52
				align center center
				frame lua "specialistOrSelectedFrame(rowNumber)"
			}
			label
			{
				area 0 0 52 52
				bam lua "spellBook[chargen.currentSpellLevelChoice][chargen.choose_spell[rowNumber].key].groundicon"
				align center center
			}
		}
		column
		{
			width 84
			label
			{
				area 8 0 -1 -1
				text lua "Infinity_FetchString(spellBook[chargen.currentSpellLevelChoice][chargen.choose_spell[rowNumber].key].name)"
				text style "normal"
				align left center
			}
		}
		scrollbar 'LUISCRC'
		rowheight 52
		hidehighlight
		area 42 192 340 414
		table "chargen.choose_spell"
		var currentChargenChooseMageSpell
		action
		"
			if not chargen.choose_spell[currentChargenChooseMageSpell].known then
				createCharScreen:OnLearnMageSpellButtonClick(currentChargenChooseMageSpell)
			end
			needSpecialist = false
			if chargen.extraSpells == 0 and not createCharScreen:IsDoneButtonClickable() then
				createCharScreen:OnLearnMageSpellButtonClick(currentChargenChooseMageSpell)
				needSpecialist = true
			end
		"
	}
	label
	{
		area 274 56 336 48
		text lua "t('SPELLS_REMAINING_NORMAL') .. ': ' .. chargen.extraSpells"
		text style "normal"
		text align center center
	}

	text
	{
		area 436 128 382 478
		text lua "chooseSpellOrGeneralHelp()"
		text style "normal"
		scrollbar 'LUISCRC'
	}
	button
	{
		bam LUIOSTUM
		area 111 120 201 44
		text style "button"
		text lua "Infinity_FetchString(engine_mode == 0 and 24411 or 34210)"
		clickable lua "chargen.extraSpells > 0"
		action
		"
			autopickSpells()
		"
	}
	button
	{
		on return
		bam LUIOSTUL
		area 210 653 234 44
		text style "button"
		text lua "nextOrDone()"
		clickable lua "createCharScreen:IsDoneButtonClickable()"
		action
		"
			createCharScreen:OnDoneButtonClick()
		"
	}
	button
	{
		on escape
		bam LUIOSTUR
		area 464 653 234 44
		text style "button"
		text "BACK_BUTTON"
		action
		"
			Infinity_PopMenu()
			createCharScreen:OnCancelButtonClick()
		"
	}
}
