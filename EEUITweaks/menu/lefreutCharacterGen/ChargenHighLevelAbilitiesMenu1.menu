menu
{
	name 'CHARGEN_HIGH_LEVEL_ABILITIES'
	align center center
	ignoreesc
	onOpen
	"
		currentHLASelection = nil
	"
	label
	{
		area 0 0 864 710
		mosaic LUICUSTB
	}
	label
	{
		area 82 12 700 40
		text "LEVEL_UP_TITLE"
		text style "title"
	}
	label
	{
		area 46 120 286 44
		text "HIGH_LEVEL_ABILITIES_TITLE"
		text style "label"
		text align center center
	}
	label
	{
		area 332 120 50 44
		text lua "chargen.numHLAs"
		text style "label"
		text align center center
	}
	text
	{
		area 436 128 382 478
		text lua "Infinity_FetchString(chargenHLADescription())"
		text style "normal"
		scrollbar 'GUISCRC'
	}

	list
	{
		column
		{
			width 16
			label
			{
				bam LUIBTBUT
				area 0 0 52 52
				align center center
				icon lua "chargen.HLAs[rowNumber].icon"
				count lua "chargen.HLAs[rowNumber].count"
			}
		}
		column
		{
			width 56
			label
			{
				area 4 0 -1 -1
				text lua "Infinity_FetchString(chargen.HLAs[rowNumber].name)"
				text style "normal"
				align left center
			}
		}
		column
		{
			width 14
			label
			{
				area 0 4 45 42
				bam LUIOSW
				frame lua "chargenHLAPlusMinusFrame(chargenReverseButtons, 3, rowNumber)"
				sequence lua "chargenReverseButtons and 1 or 3"
			}
		}
		column
		{
			width 14
			label
			{
				area 0 4 45 42
				bam LUIOSW
				frame lua "chargenHLAPlusMinusFrame(not chargenReverseButtons, 4, rowNumber)"
				sequence lua "chargenReverseButtons and 0 or 4"
			}
		}
		scrollbar 'GUISCRC'
		rowheight 52
		hidehighlight
		area 42 192 340 414
		table "chargen.HLAs"
		var currentHLASelection
		action
		"
			local delta = chargenReverseButtons and -1 or 1
			if cellNumber == 3 and chargenHLAPlusMinusFrame(chargenReverseButtons, 3, currentHLASelection) ~= 3 then
				Infinity_PlaySound('GAM_09')
				createCharScreen:OnHLAButtonClick(currentHLASelection, delta)
			elseif cellNumber == 4 and chargenHLAPlusMinusFrame(not chargenReverseButtons, 4, currentHLASelection) ~= 3 then
				Infinity_PlaySound('GAM_09')
				createCharScreen:OnHLAButtonClick(currentHLASelection, -delta)
			end
			cellNumber = nil
		"
	}

	button
	{
		on enter
		bam LUIOSTCL
		area 308 653 300 44
		text style "button"
		text "DONE_BUTTON"
		clickable lua "createCharScreen:IsDoneButtonClickable()"
		action
		"
			createCharScreen:OnDoneButtonClick()
			createCharScreen:OnDoneButtonClick()
		"
	}
}
