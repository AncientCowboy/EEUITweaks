menu
{
	name 'CHARGEN_KIT'
	modal
	align center center
	ignoreesc
	onOpen
	"
		currentChargenKit = 1
		Infinity_PlaySound('GAM_03')
	"
	onClose
	"
		Infinity_PlaySound('GAM_04')
	"
	label
	{
		area 0 0 864 710
		mosaic LUICUSTB
	}
	label
	{
		area 82 12 700 40
		text "CLASS_KIT_TITLE"
		text style "title"
	}
	list
	{
		column
		{
			width 100
			button
			{
				area 0 0 356 32
				bam LUICHLNG
				frame lua "rowNumber == currentChargenKit and 2 or 0"
			}
			button
			{
				area 0 0 340 32
				text lua "Infinity_FetchString(chargen.kit[rowNumber].name)"
				text style "button"
				pad 8 8 8 8
			}
		}
		action
		"
			if chargen.kit[currentChargenKit] then
				Infinity_PlaySound('GAM_09')
				createCharScreen:OnKitSelectButtonClick(chargen.kit[currentChargenKit].id)
			else
				currentChargenKit = nil
			end
		"
		rowheight 34
		hidehighlight
		area 41 192 356 414
		table "chargen.kit"
		var currentChargenKit
		scrollbar 'GUISCRC'
	}
	text
	{
		area 436 128 382 478
		text lua "kitOrGeneralHelp()"
		text style "normal"
		scrollbar 'GUISCRC'
	}
	button
	{
		on return
		bam LUIOSTUL
		area 210 653 234 44
		text style "button"
		text "DONE_BUTTON"
		clickable lua "createCharScreen:IsDoneButtonClickable() and currentChargenKit"
		action
		"
			Infinity_PopMenu()
			createCharScreen:OnDoneButtonClick()
		"
	}
	button
	{
		on escape
		bam LUIOSTUR
		area 464 653 234 44
		text style "button"
		text "BACK_BUTTON"
		action
		"
			Infinity_PopMenu()
			createCharScreen:OnCancelButtonClick()
		"
	}
}
