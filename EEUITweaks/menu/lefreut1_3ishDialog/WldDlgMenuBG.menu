menu
{
	name 'WORLD_DIALOG'
	offset 0 -54
	align center bottom
	ignoreEsc
	onOpen
	"
		Infinity_PushMenu('WORLD_DIALOG_CONFIRM')
		if(isTouchActionbar() and not dialogViewMode) then
			Infinity_PushMenu('WORLD_DIALOG_LEFT')
			Infinity_PushMenu('WORLD_DIALOG_RIGHT')
		end
		greySidebars()
	"
	onClose
	"
		Infinity_PopMenu('WORLD_DIALOG_CONFIRM')
		Infinity_PopMenu('WORLD_DIALOG_LEFT')
		Infinity_PopMenu('WORLD_DIALOG_RIGHT')
		ungreySidebars()
	"
	label
	{
		name "worldDialogBackground"
		area 0 0 864 250
		rectangle 4
	}
	button
	{
		on pageup
		action
		"
			dragDialogMessagesY(-20)
		"
	}
	button
	{
		on pagedown
		action
		"
			dragDialogMessagesY(20)
		"
	}
	label
	{
		name "worldDialogPortraitArea"
		area 24 20 54 84
		bitmap lua "worldNPCDialogPortrait"
	}
	handle
	{
		name "dialogHandleY"
		area 0 0 864 20
		actionDrag
		"
			dragDialogMessagesY(motionY)
		"
	}
	list
	{
		column
		{
			width 100
			text
			{
				enabled "rowNumber == 1"
				opacity lua "step == 1 and 0 or 255"
				area 0 0 -1 -1
				text lua "getDialogText()"
				text style "normal"
			}
			text
			{
				enabled "rowNumber == 2"
				area 0 0 -1 -1
				pad 0 0 0 12
				text lua "worldNPCDialogText:gsub('\n', ': ', 1)"
				text style "normal"
			}
			label
			{
				area 0 0 -1 -1
				enabled "rowNumber > 2 and rowNumber < #worldPlayerDialogChoices + 3 and dialogEntryGreyed()"
				rectangle 1
				rectangle opacity 100
			}
			text
			{
				enabled "rowNumber > 2 and rowNumber < #worldPlayerDialogChoices + 3"
				area 0 0 -1 -1
				text lua "getDialogEntryText(rowNumber)"
				text style "normal"
			}
			text
			{
				enabled "rowNumber == #worldPlayerDialogChoices + 3"
				area 0 0 -1 -1
				text lua "getDialogPaddingText()"
				text style "normal"
			}
		}
		name "worldPlayerDialogChoicesList"
		area 88 20 752 210
		rowheight dynamic
		hideHighlight
		table "makeDialogTable()"
		var "worldPlayerDialogSelection"
		scrollbar 'GUISCRC'
		scrollbar func "dialogScroll"
		actionEnter
		"
			if(gameOptions.m_bConfirmDialog == true) then return end
			worldPlayerDialogSelection = mouseoverRow
		"
		actionExit
		"
			if(gameOptions.m_bConfirmDialog == true) then return end
			worldPlayerDialogSelection = 0
		"
		action
		"
			--In confirm mode, just highlight the choice.
			if(gameOptions.m_bConfirmDialog == true) then return end
			if(worldPlayerDialogChoices[worldPlayerDialogSelection - 2] == nil) then return end
			worldScreen:OnDialogReplyClick(worldPlayerDialogChoices[worldPlayerDialogSelection - 2].marker)
			worldPlayerDialogSelection = 0
		"
	}
}
