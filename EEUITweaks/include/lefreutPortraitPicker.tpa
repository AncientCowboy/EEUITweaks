/*******************************************************************************
 * lefreutPortraitPicker.ChargenPatch1
 * Adds 'onOpen "gender = 2" ' code to passed CHARGEN
 *
 * On completion $UIMenuIF("Succeeded") 1 => Success, 0 => Failure
 * 
 ******************************************************************************/
DEFINE_ACTION_MACRO lefreutPortraitPicker.ChargenPatch1
BEGIN
	LOCAL_SPRINT tempstr $UIMenuIF("Body")
	LOCAL_SPRINT adder ~%TAB%onOpen%LE%%TAB%"%LE%%TAB%%TAB%gender = 2%LE%%TAB%"%LE%~
	LOCAL_SPRINT adder2 ~%LE%%TAB%%TAB%gender = 2%LE%%TAB%~
	LOCAL_SET found = 0

	// Put current menu definition into inline file
	COPY - ~ToInlineTemplate~ ~.../CounterInLine~
		REPLACE_EVALUATE ~dummy~ BEGIN END
		~%tempstr%~
	OUTER_SPRINT tempstr ~~

	// Allow for other patches that may have already added onopen - first single quote delimited
	COPY - ~.../CounterInLine~ ~.../CounterInLine~
		REPLACE_EVALUATE CASE_INSENSITIVE ~\(^[%TAB% ]*onopen%MWHITE%%MSQUOTE%%MNOTSQUOTE%*\)\(%MSQUOTE%.*%MEOL%\)~
		BEGIN
			SPRINT tempstr ~%MATCH1%%adder2%%MATCH2%~
			SET found = 1
		END
		~%tempstr%~

	ACTION_IF NOT %found% THEN
	BEGIN // Try for double quote
		COPY - ~.../CounterInLine~ ~.../CounterInLine~
			REPLACE_EVALUATE CASE_INSENSITIVE ~\(^[%TAB% ]*onopen%MWHITE%%MDQUOTE%%MNOTDQUOTE%*\)\(%MDQUOTE%.*%MEOL%\)~
			BEGIN
				SPRINT tempstr ~%MATCH1%%adder2%%MATCH2%~
				SET found = 1
			END
			~%tempstr%~

	END

	ACTION_IF NOT %found% THEN
	BEGIN // No existing onopen - Add code after ignoreesc
		COPY - ~.../CounterInLine~ ~.../CounterInLine~
			REPLACE_EVALUATE CASE_INSENSITIVE ~^[%TAB% ]*ignoreesc.*%MEOL%~
			BEGIN
				SPRINT tempstr ~%MATCH0%%adder%~
				SET found = 1
			END
			~%tempstr%~
	END

	// Not found - give up
	ACTION_IF NOT %found% THEN
	BEGIN
		LOG ~In lefreutPortraitPicker.ChargenPatch1 - failed to insert onopen code~
		OUTER_SET $UIMenuIF("Succeeded") = 0
	END ELSE 

	// Success somewhere - copy .../CounterInLine to $UIMenuIF("Body")
	BEGIN
		OUTER_SPRINT $UIMenuIF("Body") ~~
		OUTER_SET found = SIZE_OF_FILE ~.../CounterInLine~
		COPY - ~.../CounterInLine~ ~.../CounterInLine~
			READ_ASCII 0 $UIMenuIF("Body") (%found%-1)
		OUTER_SET $UIMenuIF("Succeeded") = 1
	END
	COPY - ~EmptyInlineTemplate~ ~.../CounterInline~
END

/******************************************************************************
 * lefreut's Portrait Picker Installation
 *****************************************************************************/
// Open UI.MENU
LOG @12
SILENT
LAUNCH_ACTION_MACRO UIMenuOpen
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @20
END

// Add CHARACTER_PORTRAIT
LOG @108001
SILENT
OUTER_SPRINT $UIMenuIF("MenuName") ~CHARGEN_PORTRAIT~
OUTER_SPRINT $UIMenuIF("Body") ~~
ACTION_IF IsIWDEE THEN
BEGIN
	OUTER_SPRINT $UIMenuIF("Filespec") ~%PKGNAME%/menu/lefreutPortraitPicker/ChargenPortraitMixed3.menu~
END ELSE ACTION_IF IsSoD OR IsEETSoD OR IsSoDOverhaul OR IsKRDS OR IsKRDS0 OR IsKRDS1 OR IsKRDS2 OR IsKRDS3 OR IsKRDS4 OR IsKRDS5 THEN
BEGIN
	OUTER_SPRINT $UIMenuIF("Filespec") ~%PKGNAME%/menu/lefreutPortraitPicker/ChargenPortraitMixed2.menu~
END ELSE
BEGIN
	OUTER_SPRINT $UIMenuIF("Filespec") ~%PKGNAME%/menu/lefreutPortraitPicker/ChargenPortraitMixed1.menu~
END
LAUNCH_ACTION_MACRO UIMenuReplaceMenuMixed
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @108002
END

// Replace CHARGEN_GENDER
LOG @108003
SILENT
OUTER_SPRINT $UIMenuIF("MenuName") ~CHARGEN_GENDER~
OUTER_SPRINT $UIMenuIF("Body") ~~
OUTER_SPRINT $UIMenuIF("Filespec") ~%PKGNAME%/menu/lefreutPortraitPicker/ChargenGender1.menu~
LAUNCH_ACTION_MACRO UIMenuReplaceMenu
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @108004
END

// Patch CHARGEN menu
LOG @149
SILENT

OUTER_SPRINT $UIMenuIF("MenuName") ~CHARGEN~
OUTER_SPRINT $UIMenuIF("Body") ~~
LAUNCH_ACTION_MACRO UIMenuGetMenu
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @150
END

LAUNCH_ACTION_MACRO lefreutPortraitPicker.ChargenPatch1
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @151
END

LAUNCH_ACTION_MACRO UIMenuUpdateMenu
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @103
END

// Close UI.MENU
LOG @13
SILENT
LAUNCH_ACTION_MACRO UIMenuClose
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @21
END


// Copy common UI resources
LOG @17
ACTION_IF IsBGEE THEN
BEGIN
	OUTER_SPRINT theme ~BGEETheme~
END
ACTION_IF IsSoD OR IsEETSoD OR IsSoDOverhaul OR IsKRDS OR IsKRDS0 OR IsKRDS1 OR IsKRDS2 OR IsKRDS3 OR IsKRDS4 OR IsKRDS5 THEN
BEGIN
	OUTER_SPRINT theme ~SODTheme~
END
ACTION_IF IsBG2 OR IsBG2onSoD OR IsEETBG2 THEN
BEGIN
	OUTER_SPRINT theme ~BG2Theme~
END
ACTION_IF IsIWDEE THEN
BEGIN
	OUTER_SPRINT theme ~IWDTheme~
END

SILENT
ACTION_IF NOT IsIWDEE THEN
BEGIN
LAF EEUITweaks.MultiCopyPVRZResource
	STR_VAR
		source_folder = EVAL ~%PKGNAME%/copy/luicommon/%theme%~
		resource_file = ~LUIOSTUL.BAM LUIOSTUR.BAM~
		pvrz_files = ~MOS4004.PVRZ~
	RET
		$UIMenuIF("Succeeded") = succeeded
END
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @44
END

LAF EEUITweaks.CopyPVRZResource
	STR_VAR
		source_folder = EVAL ~%PKGNAME%/copy/luicommon/%theme%~
		resource_file = ~LUIRSP10.BAM~
		pvrz_files = ~MOS4010.PVRZ~
	RET
		$UIMenuIF("Succeeded") = succeeded
END
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @44
END
END

LAF EEUITweaks.CopyPVRZResource
	STR_VAR
		source_folder = EVAL ~%PKGNAME%/copy/luicommon/%theme%~
		resource_file = ~LUIGEND1.BAM~
		pvrz_files = ~MOS4015.PVRZ~
	RET
		$UIMenuIF("Succeeded") = succeeded
END
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @44
END

LAF EEUITweaks.CopyPVRZResource
	STR_VAR
		source_folder = EVAL ~%PKGNAME%/copy/luicommon/%theme%~
		resource_file = ~LUIAPBR.MOS~
		pvrz_files = ~MOS4249.PVRZ~
	RET
		$UIMenuIF("Succeeded") = succeeded
END
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @44
END
