
/*******************************************************************************
 * WorldDialogConfirmPatch1
 * 
 * Changes alignment and sets offset
 *
 * On completion $UIMenuIF("Succeeded") 1 => Success, 0 => Failure
 * 
 ******************************************************************************/
DEFINE_ACTION_MACRO CenteredDialog.WorldDialogConfirmPatch1
BEGIN
	LOCAL_SPRINT tempstr $UIMenuIF("Body")
	LOCAL_SET found = 0
	LOCAL_SPRINT adder ~%TAB%offset 0 116%LE%%TAB%align center center%LE%~

	// Put current menu definition into inline file
	COPY - ~ToInlineTemplate~ ~.../CounterInLine~
		REPLACE_EVALUATE ~dummy~ BEGIN END
		~%tempstr%~
	OUTER_SPRINT tempstr ~~

	// Patch
	COPY - ~.../CounterInLine~ ~.../CounterInLine~
		REPLACE_EVALUATE CASE_INSENSITIVE ~^[%TAB% ]*align[%TAB% ]+center[%TAB% ]+bottom.*%MEOL%\([%TAB% ]*offset[%TAB% ]+[-0-9]+[%TAB% ]+[-0-9]+.*%MEOL%\)?~
		BEGIN
			SET found = 1
		END
		~%adder%~

	// Not found - fail
	ACTION_IF NOT %found% THEN
	BEGIN
		LOG ~In CenteredDialog.WorldDialogConfirmPatch1 - failed to find alignment clause~
		OUTER_SET $UIMenuIF("Succeeded") = 0
	END ELSE 

	// Success somewhere - copy .../CounterInLine to $UIMenuIF("Body")
	BEGIN
		OUTER_SPRINT $UIMenuIF("Body") ~~
		OUTER_SET found = SIZE_OF_FILE ~.../CounterInLine~
		COPY - ~.../CounterInLine~ ~.../CounterInLine~
			READ_ASCII 0 $UIMenuIF("Body") (%found%-1)
		OUTER_SET $UIMenuIF("Succeeded") = 1
	END
	COPY - ~EmptyInlineTemplate~ ~.../CounterInline~
END

/*******************************************************************************
 * WorldDialogConfirmPatch2
 * 
 * Changes alignment and sets offset for LeUI WORLD_CONFIRM
 *
 * On completion $UIMenuIF("Succeeded") 1 => Success, 0 => Failure
 * 
 ******************************************************************************/
DEFINE_ACTION_MACRO CenteredDialog.WorldDialogConfirmPatch2
BEGIN
	LOCAL_SPRINT tempstr $UIMenuIF("Body")
	LOCAL_SET found = 0
	LOCAL_SPRINT adder ~%TAB%offset 0 93%LE%%TAB%align center center%LE%~

	// Put current menu definition into inline file
	COPY - ~ToInlineTemplate~ ~.../CounterInLine~
		REPLACE_EVALUATE ~dummy~ BEGIN END
		~%tempstr%~
	OUTER_SPRINT tempstr ~~

	// First, remove any offset
	COPY - ~.../CounterInLine~ ~.../CounterInLine~
		REPLACE_EVALUATE CASE_INSENSITIVE ~^[%TAB% ]*offset[%TAB% ]+[-0-9]+[%TAB% ]+[-0-9]+.*%MEOL%~
		BEGIN
			SET found = 1
		END
		~~
	OUTER_SET found = 0 // Ignore not found
	// Patch desired alignment and offset over alignment
	COPY - ~.../CounterInLine~ ~.../CounterInLine~
		REPLACE_EVALUATE CASE_INSENSITIVE ~^[%TAB% ]*align[%TAB% ]+center[%TAB% ]+bottom.*%MEOL%~
		BEGIN
			SET found = 1
		END
		~%adder%~

	// Not found - fail
	ACTION_IF NOT %found% THEN
	BEGIN
		LOG ~In CenteredDialog.WorldDialogConfirmPatch2 - failed to find alignment clause~
		OUTER_SET $UIMenuIF("Succeeded") = 0
	END ELSE 

	// Success somewhere - copy .../CounterInLine to $UIMenuIF("Body")
	BEGIN
		OUTER_SPRINT $UIMenuIF("Body") ~~
		OUTER_SET found = SIZE_OF_FILE ~.../CounterInLine~
		COPY - ~.../CounterInLine~ ~.../CounterInLine~
			READ_ASCII 0 $UIMenuIF("Body") (%found%-1)
		OUTER_SET $UIMenuIF("Succeeded") = 1
	END
	COPY - ~EmptyInlineTemplate~ ~.../CounterInline~
END

/*******************************************************************************
 * WorldDialogPatch1
 * 
 * Changes alignment and removes offset for LeUI WORLD_CONFIRM
 *
 * On completion $UIMenuIF("Succeeded") 1 => Success, 0 => Failure
 * 
 ******************************************************************************/
DEFINE_ACTION_MACRO CenteredDialog.WorldDialogPatch1
BEGIN
	LOCAL_SPRINT tempstr $UIMenuIF("Body")
	LOCAL_SET found = 0
	LOCAL_SPRINT adder ~%TAB%align center center%LE%~

	// Put current menu definition into inline file
	COPY - ~ToInlineTemplate~ ~.../CounterInLine~
		REPLACE_EVALUATE ~dummy~ BEGIN END
		~%tempstr%~
	OUTER_SPRINT tempstr ~~

	// First, remove any offset
	COPY - ~.../CounterInLine~ ~.../CounterInLine~
		REPLACE_EVALUATE CASE_INSENSITIVE ~^[%TAB% ]*offset[%TAB% ]+[-0-9]+[%TAB% ]+[-0-9]+.*%MEOL%~
		BEGIN
			SET found = 1
		END
		~~
	OUTER_SET found = 0 // Ignore not found
	// Patch desired alignment
	COPY - ~.../CounterInLine~ ~.../CounterInLine~
		REPLACE_EVALUATE CASE_INSENSITIVE ~^[%TAB% ]*align[%TAB% ]+center[%TAB% ]+bottom.*%MEOL%~
		BEGIN
			SET found = 1
		END
		~%adder%~

	// Not found - fail
	ACTION_IF NOT %found% THEN
	BEGIN
		LOG ~In CenteredDialog.WorldDialogPatch1 - failed to find alignment clause~
		OUTER_SET $UIMenuIF("Succeeded") = 0
	END ELSE 

	// Success somewhere - copy .../CounterInLine to $UIMenuIF("Body")
	BEGIN
		OUTER_SPRINT $UIMenuIF("Body") ~~
		OUTER_SET found = SIZE_OF_FILE ~.../CounterInLine~
		COPY - ~.../CounterInLine~ ~.../CounterInLine~
			READ_ASCII 0 $UIMenuIF("Body") (%found%-1)
		OUTER_SET $UIMenuIF("Succeeded") = 1
	END
	COPY - ~EmptyInlineTemplate~ ~.../CounterInline~
END

/*******************************************************************************
 * Centered Dialog Installation
 ******************************************************************************/
// Open UI.MENU
LOG @12
SILENT
LAUNCH_ACTION_MACRO UIMenuOpen
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @20
END

// Patch WORLD_DIALOG_CONFIRM
LOG @185
SILENT

OUTER_SPRINT $UIMenuIF("MenuName") ~WORLD_DIALOG_CONFIRM~
OUTER_SPRINT $UIMenuIF("Body") ~~
LAUNCH_ACTION_MACRO UIMenuGetMenu
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @186
END

ACTION_IF ((MOD_IS_INSTALLED ~EEUITweaks.tp2~ ~2060~) OR (IsLeUI = 1)) THEN
BEGIN
	LAUNCH_ACTION_MACRO CenteredDialog.WorldDialogConfirmPatch2
END ELSE
BEGIN
	LAUNCH_ACTION_MACRO CenteredDialog.WorldDialogConfirmPatch1
END

ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @187
END

LAUNCH_ACTION_MACRO UIMenuUpdateMenu
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @103
END

// Replace or patch WORLD_DIALOG menu
OUTER_SPRINT $UIMenuIF("MenuName") ~WORLD_DIALOG~
OUTER_SPRINT $UIMenuIF("Body") ~~
ACTION_IF ((MOD_IS_INSTALLED ~EEUITweaks.tp2~ ~2060~) OR (IsLeUI = 1)) THEN
BEGIN // Patch
	LOG @205007
	SILENT
	LAUNCH_ACTION_MACRO UIMenuGetMenu
	ACTION_IF NOT $UIMenuIF("Succeeded") THEN
	BEGIN
		FAIL @205004
	END
	LAUNCH_ACTION_MACRO CenteredDialog.WorldDialogPatch1
	ACTION_IF NOT $UIMenuIF("Succeeded") THEN
	BEGIN
		FAIL @205005
	END
	LAUNCH_ACTION_MACRO UIMenuUpdateMenu
	ACTION_IF NOT $UIMenuIF("Succeeded") THEN
	BEGIN
		FAIL @103
	END
END ELSE
BEGIN // Replace
	LOG @205001
	SILENT
	ACTION_MATCH 1
	WITH
		%IsBG2% %IsBGEE% %IsEETBG2%
		BEGIN
			OUTER_SPRINT $UIMenuIF("Filespec") ~%PKGNAME%/menu/CenteredDialog/worlddialogmenu2.menu~
		END
		%IsEETSoD%
		BEGIN
			OUTER_SPRINT $UIMenuIF("Filespec") ~%PKGNAME%/menu/CenteredDialog/worlddialogmenu3.menu~
		END
		%IsKRDS% %IsKRDS0%
		BEGIN
			OUTER_SPRINT $UIMenuIF("Filespec") ~%PKGNAME%/menu/CenteredDialog/worlddialogmenu4.menu~
		END
		%IsSoD% %IsBG2onSoD% %IsKRDS1% %IsKRDS2% %IsKRDS3% %IsKRDS4% %IsKRDS5% WHEN NOT(%IsKRDS0%)
		BEGIN
			OUTER_SPRINT $UIMenuIF("Filespec") ~%PKGNAME%/menu/CenteredDialog/worlddialogmenu.menu~
		END
		%IsTIWDonBG2% %IsTIWD2onBG2%
		BEGIN
			OUTER_SPRINT $UIMenuIF("Filespec") ~%PKGNAME%/menu/CenteredDialog/worlddialogmenu5.menu~
		END
		%IsTIWDonSoD%
		BEGIN
			OUTER_SPRINT $UIMenuIF("Filespec") ~%PKGNAME%/menu/CenteredDialog/worlddialogmenu6.menu~
		END
		DEFAULT // Nothing left except Fail
			FAIL @33

	END
	LAUNCH_ACTION_MACRO UIMenuReplaceMenu
	ACTION_IF NOT $UIMenuIF("Succeeded") THEN
	BEGIN
		FAIL @205002
	END
END

// Close up
LOG @13
SILENT
LAUNCH_ACTION_MACRO UIMenuClose
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @21
END



