
/*******************************************************************************
 * WorldDialogConfirmPatch1
 * 
 * Changes alignment and sets offset
 *
 * On completion $UIMenuIF("Succeeded") 1 => Success, 0 => Failure
 * 
 ******************************************************************************/
DEFINE_ACTION_MACRO CenteredDialog.WorldDialogConfirmPatch1
BEGIN
	LOCAL_SPRINT tempstr $UIMenuIF("Body")
	LOCAL_SET found = 0
	LOCAL_SPRINT adder ~%TAB%offset 0 116%LE%%TAB%align center center%LE%~

	// Put current menu definition into inline file
	COPY - ~ToInlineTemplate~ ~.../CounterInLine~
		REPLACE_EVALUATE ~dummy~ BEGIN END
		~%tempstr%~
	OUTER_SPRINT tempstr ~~

	// Patch
	COPY - ~.../CounterInLine~ ~.../CounterInLine~
		REPLACE_EVALUATE CASE_INSENSITIVE ~^[%TAB% ]*align[%TAB% ]+center[%TAB% ]+bottom.*%MEOL%~
		BEGIN
			SET found = 1
		END
		~%adder%~

	// Not found - fail
	ACTION_IF NOT %found% THEN
	BEGIN
		LOG ~In CenteredDialog.WorldDialogConfirmPatch1 - failed to find alignment clause~
		OUTER_SET $UIMenuIF("Succeeded") = 0
	END ELSE 

	// Success somewhere - copy .../CounterInLine to $UIMenuIF("Body")
	BEGIN
		OUTER_SPRINT $UIMenuIF("Body") ~~
		OUTER_SET found = SIZE_OF_FILE ~.../CounterInLine~
		COPY - ~.../CounterInLine~ ~.../CounterInLine~
			READ_ASCII 0 $UIMenuIF("Body") (%found%-1)
		OUTER_SET $UIMenuIF("Succeeded") = 1
	END
	COPY - ~EmptyInlineTemplate~ ~.../CounterInline~
END

/*******************************************************************************
 * Centered Dialog Installation
 ******************************************************************************/
// Open UI.MENU
PRINT @12
SILENT
LAUNCH_ACTION_MACRO UIMenuOpen
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @20
END

// Patch WORLD_DIALOG_CONFIRM
PRINT @185
SILENT

OUTER_SPRINT $UIMenuIF("MenuName") ~WORLD_DIALOG_CONFIRM~
OUTER_SPRINT $UIMenuIF("Body") ~~
LAUNCH_ACTION_MACRO UIMenuGetMenu
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @186
END

LAUNCH_ACTION_MACRO CenteredDialog.WorldDialogConfirmPatch1
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @187
END

LAUNCH_ACTION_MACRO UIMenuUpdateMenu
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @103
END

// Replace WORLD_DIALOG menu
PRINT @205001
SILENT

OUTER_SPRINT $UIMenuIF("MenuName") ~WORLD_DIALOG~
ACTION_IF IsBG2 OR IsBGEE OR IsEETBG2 THEN
BEGIN
	OUTER_SPRINT $UIMenuIF("Filespec") ~%PKGNAME%/menu/CenteredDialog/worlddialogmenu2.menu~
END ELSE
ACTION_IF IsEETSoD THEN
BEGIN
	OUTER_SPRINT $UIMenuIF("Filespec") ~%PKGNAME%/menu/CenteredDialog/worlddialogmenu3.menu~
END ELSE
BEGIN
	OUTER_SPRINT $UIMenuIF("Filespec") ~%PKGNAME%/menu/CenteredDialog/worlddialogmenu.menu~
END
LAUNCH_ACTION_MACRO UIMenuReplaceMenu
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @205002
END

// Close up
PRINT @13
SILENT
LAUNCH_ACTION_MACRO UIMenuClose
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @21
END



