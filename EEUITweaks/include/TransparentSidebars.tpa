
/*******************************************************************************
 * PatchRightSideBar
 *
 * Replaces 2 background labels in RIGHT_SIDEBAR menu with a single unsized
 * placeholder label. Maintains label name from the first (top) label (in case
 * it is referenced somewhere) but with no dimensions or background
 * 
 * Results in $UIMenuIF("Succeeded") 1 => Success, 0 => Failure
 *
 ******************************************************************************/
DEFINE_ACTION_MACRO TransparentSidebars.PatchRightSideBar
BEGIN
	LOCAL_SPRINT tempstr $UIMenuIF("Body")
	LOCAL_SPRINT replacement ~%TAB%label%LE%%TAB%{%LE%%TAB%name 'rightSidebarBackground'%LE%%TAB%%TAB%area 0 0 80 1536%LE%%TAB%}%LE%~
	LOCAL_SET found = 0

	// Put current menu body into inline file
	COPY - ~ToInlineTemplate~ ~.../CounterInLine~
		REPLACE_EVALUATE ~dummy~ BEGIN END
		~%tempstr%~
	OUTER_SPRINT tempstr ~~

	// Search and patch the labels
	COPY - ~.../CounterInLine~ ~.../CounterInLine~
		REPLACE_EVALUATE ~^[%TAB% ]*label%MWHITE%{%MNOBRACE%+name[%TAB% ]+%MQUOTE%[%TAB% ]*rightSidebarBackground%MNOBRACE%+}%MWHITE%label%MWHITE%{%MNOBRACE%+mosaic[%TAB% ]+GUIWRSP%MNOBRACE%+}.*%MEOL%~
		BEGIN
			SET found = 1
		END
		~%replacement%~

	// Not found - give up
	ACTION_IF NOT %found% THEN
	BEGIN
		LOG ~In TransparentSidebars.PatchRightSideBar - failed to find the background labels. UI.MENU too unusual to handle - bailing out~
		OUTER_SET $UIMenuIF("Succeeded") = 0
	END ELSE 

	// Success - copy .../CounterInLine to $UIMenuIF("Body")
	BEGIN
		OUTER_SPRINT $UIMenuIF("Body") ~~
		OUTER_SET found = SIZE_OF_FILE ~.../CounterInLine~
		COPY - ~.../CounterInLine~ ~.../CounterInLine~
			READ_ASCII 0 $UIMenuIF("Body") (%found%-1)
		OUTER_SET $UIMenuIF("Succeeded") = 1
	END
	COPY - ~EmptyInlineTemplate~ ~.../CounterInline~
END

/*******************************************************************************
 * PatchLeftSideBar
 *
 * Replaces 2 background labels in LEFT_SIDEBAR menu with a single unsized
 * placeholder label. Maintains label name from the first (top) label (in case
 * it is referenced somewhere) but with no dimensions or background
 * 
 * Results in $UIMenuIF("Succeeded") 1 => Success, 0 => Failure
 *
 ******************************************************************************/
DEFINE_ACTION_MACRO TransparentSidebars.PatchLeftSideBar
BEGIN
	LOCAL_SPRINT tempstr $UIMenuIF("Body")
	LOCAL_SPRINT replacement ~%TAB%label%LE%%TAB%{%LE%%TAB%name 'leftSidebarBackground'%LE%%TAB%%TAB%area 0 0 80 1536%LE%%TAB%}%LE%~
	LOCAL_SET found = 0

	// Put current menu body into inline file
	COPY - ~ToInlineTemplate~ ~.../CounterInLine~
		REPLACE_EVALUATE ~dummy~ BEGIN END
		~%tempstr%~
	OUTER_SPRINT tempstr ~~

	// Search and patch the labels
	COPY - ~.../CounterInLine~ ~.../CounterInLine~
		REPLACE_EVALUATE ~^[%TAB% ]*label%MWHITE%{%MNOBRACE%+name[%TAB% ]+%MQUOTE%[%TAB% ]*leftSidebarBackground%MNOBRACE%+}%MWHITE%label%MWHITE%{%MNOBRACE%+mosaic[%TAB% ]+GUIWLSP%MNOBRACE%+}.*%MEOL%~
		BEGIN
			SET found = 1
		END
		~%replacement%~

	// Not found - give up
	ACTION_IF NOT %found% THEN
	BEGIN
		LOG ~In TransparentSidebars.PatchLeftSideBar - failed to find the background labels. UI.MENU too unusual to handle - bailing out~
		OUTER_SET $UIMenuIF("Succeeded") = 0
	END ELSE 

	// Success - copy .../CounterInLine to $UIMenuIF("Body")
	BEGIN
		OUTER_SPRINT $UIMenuIF("Body") ~~
		OUTER_SET found = SIZE_OF_FILE ~.../CounterInLine~
		COPY - ~.../CounterInLine~ ~.../CounterInLine~
			READ_ASCII 0 $UIMenuIF("Body") (%found%-1)
		OUTER_SET $UIMenuIF("Succeeded") = 1
	END
	COPY - ~EmptyInlineTemplate~ ~.../CounterInline~
END

/*******************************************************************************
 * Transparent Sidebars Installation
 ******************************************************************************/
// Open UI.MENU
LOG @12
SILENT
LAUNCH_ACTION_MACRO UIMenuOpen
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @20
END

// Patch LEFT_SIDEBAR
LOG @113
SILENT
OUTER_SPRINT $UIMenuIF("MenuName") ~LEFT_SIDEBAR~
OUTER_SPRINT $UIMenuIF("Body") ~~
LAUNCH_ACTION_MACRO UIMenuGetMenu
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @114
END
LAUNCH_ACTION_MACRO TransparentSidebars.PatchLeftSideBar
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @115
END
LAUNCH_ACTION_MACRO UIMenuUpdateMenu
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @103
END

// Patch RIGHT_SIDEBAR
LOG @116
SILENT
OUTER_SPRINT $UIMenuIF("MenuName") ~RIGHT_SIDEBAR~
OUTER_SPRINT $UIMenuIF("Body") ~~
LAUNCH_ACTION_MACRO UIMenuGetMenu
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @117
END
LAUNCH_ACTION_MACRO TransparentSidebars.PatchRightSideBar
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @118
END
LAUNCH_ACTION_MACRO UIMenuUpdateMenu
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @103
END
	
// Close UI.MENU
LOG @13
SILENT
LAUNCH_ACTION_MACRO UIMenuClose
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @21
END
