/*******************************************************************************
 * PopupRequesterPatch1
 *
 * Adds onClose element to menu
 *
 * Returns $UIMenuIF("Succeeded") 1 => Success, 0 => Failure
 *
 ******************************************************************************/
DEFINE_ACTION_MACRO AdulImprovedStore.PopupRequesterPatch1
BEGIN
	LOCAL_SPRINT tempstr $UIMenuIF("Body")
	LOCAL_SPRINT adder ~%TAB%onClose%LE%%TAB%"%LE%%TAB%%TAB%requester.requesterFunc(0)%LE%%TAB%"%LE%~
	LOCAL_SET found = 0

	// Put current menu definition into inline file
	COPY - ~ToInlineTemplate~ ~.../CounterInLine~
		REPLACE_EVALUATE ~dummy~ BEGIN END
		~%tempstr%~
	OUTER_SPRINT tempstr ~~

	// Find onOpen, add onClose to the end
	COPY - ~.../CounterInLine~ ~.../CounterInLine~
		REPLACE_EVALUATE CASE_INSENSITIVE ~^[%TAB% ]*onopen%MWHITE%%MQUOTE%%MNOTQUOTE%*%MQUOTE%.*%MEOL%~
		BEGIN
			SPRINT tempstr ~%MATCH0%%adder%~
			SET found = 1
		END
		~%tempstr%~

	// Not found - give up
	ACTION_IF NOT %found% THEN
	BEGIN
		LOG ~In AdulImprovedStore.PopupRequesterPatch1 - failed to find onopen code~
		OUTER_SET $UIMenuIF("Succeeded") = 0
	END ELSE 

	// Success somewhere - copy .../CounterInLine to $UIMenuIF("Body")
	BEGIN
		OUTER_SPRINT $UIMenuIF("Body") ~~
		OUTER_SET found = SIZE_OF_FILE ~.../CounterInLine~
		COPY - ~.../CounterInLine~ ~.../CounterInLine~
			READ_ASCII 0 $UIMenuIF("Body") (%found%-1)
		OUTER_SET $UIMenuIF("Succeeded") = 1
	END
	COPY - ~EmptyInlineTemplate~ ~.../CounterInline~
END

/*******************************************************************************
 * PopupRequesterPatch2
 *
 * Adds return as action trigger for done button
 *
 * Returns $UIMenuIF("Succeeded") 1 => Success, 0 => Failure
 *
 ******************************************************************************/
DEFINE_ACTION_MACRO AdulImprovedStore.PopupRequesterPatch2
BEGIN
	LOCAL_SPRINT tempstr $UIMenuIF("Body")
	LOCAL_SPRINT adder ~%TAB%%TAB%on return%LE%~
	LOCAL_SET found = 0

	// Put current menu definition into inline file
	COPY - ~ToInlineTemplate~ ~.../CounterInLine~
		REPLACE_EVALUATE ~dummy~ BEGIN END
		~%tempstr%~
	OUTER_SPRINT tempstr ~~

	// Find onOpen, add onClose to the end
	COPY - ~.../CounterInLine~ ~.../CounterInLine~
		REPLACE_EVALUATE CASE_INSENSITIVE ~^[%TAB% ]*action%MWHITE%%MQUOTE%%MWHITE%local[%TAB% ]+cnt[%TAB% ]*=[%TAB% ]*tonumber(itemRequestAmt).*%MEOL%~
		BEGIN
			SPRINT tempstr ~%adder%%MATCH0%~
			SET found = 1
		END
		~%tempstr%~

	// Not found - give up
	ACTION_IF NOT %found% THEN
	BEGIN
		LOG ~In AdulImprovedStore.PopupRequesterPatch2 - failed to find done action code~
		OUTER_SET $UIMenuIF("Succeeded") = 0
	END ELSE 

	// Success somewhere - copy .../CounterInLine to $UIMenuIF("Body")
	BEGIN
		OUTER_SPRINT $UIMenuIF("Body") ~~
		OUTER_SET found = SIZE_OF_FILE ~.../CounterInLine~
		COPY - ~.../CounterInLine~ ~.../CounterInLine~
			READ_ASCII 0 $UIMenuIF("Body") (%found%-1)
		OUTER_SET $UIMenuIF("Succeeded") = 1
	END
	COPY - ~EmptyInlineTemplate~ ~.../CounterInline~
END

/*******************************************************************************
 * PatchLuaBlock
 * 
 * Replaces storeSplitStack and groupSplitStack functions  
 *
 * On completion $UIMenuIF("Succeeded") 1 => Success, 0 => Failure
 * 
 ******************************************************************************/
DEFINE_ACTION_MACRO AdulImprovedStore.PatchLuaBlock
BEGIN
	LOCAL_SPRINT tempstr $UIMenuIF("Body")
	LOCAL_SPRINT adder ~~

	// Read patch insertion from buysellreplace.lua
	LOCAL_SET found = SIZE_OF_FILE ~%PKGNAME%/lua/AdulImprovedStore/buysellreplace.lua~
	COPY - ~%PKGNAME%/lua/AdulImprovedStore/buysellreplace.lua~ ~.../CounterInLine~
		READ_ASCII 0 adder (%found%)
	OUTER_SET found = 0 

	// Put code block into inline file
	COPY - ~ToInlineTemplate~ ~.../CounterInLine~
		REPLACE_EVALUATE ~dummy~ BEGIN END
		~%tempstr%~
	OUTER_SPRINT tempstr ~~

	// Find target function definitions, replace them
	COPY - ~.../CounterInLine~ ~.../CounterInLine~
		REPLACE_EVALUATE ~^[%TAB% ]*function[%TAB% ]+storeSplitStack([%TAB% ]*count[%TAB% ]*).*%MEOL%.+%MEOL%.*%MEOL%[%TAB% ]*end.*%MEOL%[%TAB% ]*function[%TAB% ]+groupSplitStack([%TAB% ]*count[%TAB% ]*).*%MEOL%.+%MEOL%.*%MEOL%[%TAB% ]*end.*%MEOL%~
		BEGIN
			SET found = 1
		END
		~%adder%~

	// If not found, nothing to do but fail.
	ACTION_IF NOT %found% THEN
	BEGIN
		LOG ~In AdulImprovedStore.PatchLuaBlock - failed to find target functions~
		OUTER_SET $UIMenuIF("Succeeded") = 0
	END

	// Success - copy .../CounterInLine to $UIMenuIF("Body")
	ACTION_IF %found% THEN
	BEGIN
		OUTER_SPRINT $UIMenuIF("Body") ~~
		OUTER_SET found = SIZE_OF_FILE ~.../CounterInLine~
		COPY - ~.../CounterInLine~ ~.../CounterInLine~
			READ_ASCII 0 $UIMenuIF("Body") (%found%-1)
			OUTER_SET $UIMenuIF("Succeeded") = 1
	END
	COPY - ~EmptyInlineTemplate~ ~.../CounterInline~
END

/*******************************************************************************
 * BuySellPatch1
 * 
 * Replaces all mouse actions of first list 
 *
 * On completion $UIMenuIF("Succeeded") 1 => Success, 0 => Failure
 * 
 ******************************************************************************/
DEFINE_ACTION_MACRO AdulImprovedStore.BuySellPatch1
BEGIN
	LOCAL_SPRINT tempstr $UIMenuIF("Body")
	LOCAL_SPRINT adder ~~

	// Read patch replacement code from buyselllist1actions.menu
	LOCAL_SET found = SIZE_OF_FILE ~%PKGNAME%/menu/AdulImprovedStore/buyselllist1actions.menu~
	COPY - ~%PKGNAME%/menu/AdulImprovedStore/buyselllist1actions.menu~ ~.../CounterInLine~
		READ_ASCII 0 adder (%found%)
	OUTER_SET found = 0 

	// Put code block into inline file
	COPY - ~ToInlineTemplate~ ~.../CounterInLine~
		REPLACE_EVALUATE ~dummy~ BEGIN END
		~%tempstr%~
	OUTER_SPRINT tempstr ~~

	// Replace target code
	COPY - ~.../CounterInLine~ ~.../CounterInLine~
		REPLACE_EVALUATE CASE_INSENSITIVE ~^[%TAB% ]+action%MWHITE%%MQUOTE%%MWHITE%if[%TAB% ]+not[%TAB% ]+storeScreen:IsCloseBagButtonClickable()%MNOTQUOTE%+%MQUOTE%%MWHITE%actionalt%MWHITE%%MQUOTE%%MNOTQUOTE%+%MQUOTE%%MWHITE%actionDbl%MWHITE%%MQUOTE%%MNOTQUOTE%+%MQUOTE%.*%MEOL%~
		BEGIN
			SET found = 1
		END
		~%adder%~

	// If not found, nothing to do but fail.
	ACTION_IF NOT %found% THEN
	BEGIN
		LOG ~In AdulImprovedStore.BuySellPatch1 - failed to find target code~
		OUTER_SET $UIMenuIF("Succeeded") = 0
	END

	// Success - copy .../CounterInLine to $UIMenuIF("Body")
	ACTION_IF %found% THEN
	BEGIN
		OUTER_SPRINT $UIMenuIF("Body") ~~
		OUTER_SET found = SIZE_OF_FILE ~.../CounterInLine~
		COPY - ~.../CounterInLine~ ~.../CounterInLine~
			READ_ASCII 0 $UIMenuIF("Body") (%found%-1)
			OUTER_SET $UIMenuIF("Succeeded") = 1
	END
	COPY - ~EmptyInlineTemplate~ ~.../CounterInline~
END

/*******************************************************************************
 * BuySellPatch2
 * 
 * Replaces action sections of second and third lists
 *
 * On completion $UIMenuIF("Succeeded") 1 => Success, 0 => Failure
 * 
 ******************************************************************************/
DEFINE_ACTION_MACRO AdulImprovedStore.BuySellPatch2
BEGIN
	LOCAL_SPRINT tempstr $UIMenuIF("Body")
	LOCAL_SPRINT adder ~~

	// Read patch replacement code from buyselllist23action.menu
	LOCAL_SET found = SIZE_OF_FILE ~%PKGNAME%/menu/AdulImprovedStore/buyselllist23action.menu~
	COPY - ~%PKGNAME%/menu/AdulImprovedStore/buyselllist23action.menu~ ~.../CounterInLine~
		READ_ASCII 0 adder (%found%)
	OUTER_SET found = 0 

	// Put code block into inline file
	COPY - ~ToInlineTemplate~ ~.../CounterInLine~
		REPLACE_EVALUATE ~dummy~ BEGIN END
		~%tempstr%~
	OUTER_SPRINT tempstr ~~

	// Replace target code
	COPY - ~.../CounterInLine~ ~.../CounterInLine~
		REPLACE_EVALUATE CASE_INSENSITIVE "^[%TAB% ]+action%MWHITE%%MQUOTE%%MWHITE%if[%TAB% ]+store.groupItems\[storeGroupItemsVar\].valid[%TAB% ]*~=[%TAB% ]*0[%TAB% ]*then%MNOTQUOTE%+%MQUOTE%.*%MEOL%"
		BEGIN
			SET found = 1
		END
		~%adder%~

	// If not found, nothing to do but fail.
	ACTION_IF NOT %found% THEN
	BEGIN
		LOG ~In AdulImprovedStore.BuySellPatch2 - failed to find target code~
		OUTER_SET $UIMenuIF("Succeeded") = 0
	END

	// Success - copy .../CounterInLine to $UIMenuIF("Body")
	ACTION_IF %found% THEN
	BEGIN
		OUTER_SPRINT $UIMenuIF("Body") ~~
		OUTER_SET found = SIZE_OF_FILE ~.../CounterInLine~
		COPY - ~.../CounterInLine~ ~.../CounterInLine~
			READ_ASCII 0 $UIMenuIF("Body") (%found%-1)
			OUTER_SET $UIMenuIF("Succeeded") = 1
	END
	COPY - ~EmptyInlineTemplate~ ~.../CounterInline~
END

/*******************************************************************************
 * Adul's Improved Store Useability Installation
 ******************************************************************************/
// Open UI.MENU
PRINT @12
SILENT
LAUNCH_ACTION_MACRO UIMenuOpen
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @20
END

// Patch POPUP_REQUESTER
PRINT @179
SILENT
OUTER_SPRINT $UIMenuIF("MenuName") ~POPUP_REQUESTER~
OUTER_SPRINT $UIMenuIF("Body") ~~
LAUNCH_ACTION_MACRO UIMenuGetMenu

ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @180
END

LAUNCH_ACTION_MACRO AdulImprovedStore.PopupRequesterPatch1
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @181
END

LAUNCH_ACTION_MACRO AdulImprovedStore.PopupRequesterPatch2
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @181
END

LAUNCH_ACTION_MACRO UIMenuUpdateMenu
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @103
END

// Patch STORE_BUYSELL Lua block
PRINT @401001
SILENT

OUTER_SPRINT $UIMenuIF("LuaRegex") ~function[%TAB% ]+getStoreSlotHighlight([^)]+)~
OUTER_SPRINT $UIMenuIF("Body") ~~
LAUNCH_ACTION_MACRO UIMenuGetMatchingLuaBlock
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @401002
END

LAUNCH_ACTION_MACRO AdulImprovedStore.PatchLuaBlock
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @401003
END
LAUNCH_ACTION_MACRO UIMenuUpdateLuaBlock
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @103
END

// Patch STORE_BUYSELL
PRINT @122
SILENT
OUTER_SPRINT $UIMenuIF("MenuName") ~STORE_BUYSELL~
OUTER_SPRINT $UIMenuIF("Body") ~~
LAUNCH_ACTION_MACRO UIMenuGetMenu

ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @123
END

LAUNCH_ACTION_MACRO AdulImprovedStore.BuySellPatch1
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @124
END

LAUNCH_ACTION_MACRO AdulImprovedStore.BuySellPatch2
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @124
END

LAUNCH_ACTION_MACRO UIMenuUpdateMenu
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @103
END

// Close UI.MENU
PRINT @13
SILENT
LAUNCH_ACTION_MACRO UIMenuClose
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @21
END

