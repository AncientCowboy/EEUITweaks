
/*******************************************************************************
 * AdulImprovedStore.PatchBody
 * 
 * In code previously read into $UIMenuIF("Body"), replaces occurances of 
 * $UIMenuIF("LuaRegex") with contents of $UIMenuIF("Filespec")  
 *
 * On completion $UIMenuIF("Succeeded") 1 => Success, 0 => Failure
 * 
 ******************************************************************************/
DEFINE_ACTION_MACRO AdulImprovedStore.PatchBody
BEGIN
	LOCAL_SPRINT tempstr $UIMenuIF("Body")
	LOCAL_SPRINT tempfs $UIMenuIF("Filespec")
	LOCAL_SPRINT adder ~~
	LOCAL_SPRINT pattern $UIMenuIF("LuaRegex")

	// Read patch insertion from $UIMenuIF("Filespec")
	LOCAL_SET found = SIZE_OF_FILE ~%tempfs%~
	COPY - ~%tempfs%~ ~.../CounterInLine~
		READ_ASCII 0 adder (%found%)
	OUTER_SET found = 0 

	// Put code block into inline file
	COPY - ~ToInlineTemplate~ ~.../CounterInLine~
		REPLACE_EVALUATE ~dummy~ BEGIN END
		~%tempstr%~
	OUTER_SPRINT tempstr ~~

	// Find target function definitions, replace them
	COPY - ~.../CounterInLine~ ~.../CounterInLine~
		REPLACE_EVALUATE ~%pattern%~
		BEGIN
			SET found = 1
		END
		~%adder%~

	// If not found, nothing to do but fail.
	ACTION_IF NOT %found% THEN
	BEGIN
		LOG ~In AdulImprovedStore.PatchBody - failed to find pattern %pattern%~
		OUTER_SET $UIMenuIF("Succeeded") = 0
	END

	// Success - copy .../CounterInLine to $UIMenuIF("Body")
	ACTION_IF %found% THEN
	BEGIN
		OUTER_SPRINT $UIMenuIF("Body") ~~
		OUTER_SET found = SIZE_OF_FILE ~.../CounterInLine~
		COPY - ~.../CounterInLine~ ~.../CounterInLine~
			READ_ASCII 0 $UIMenuIF("Body") (%found%-1)
			OUTER_SET $UIMenuIF("Succeeded") = 1
	END
	COPY - ~EmptyInlineTemplate~ ~.../CounterInline~
END

/*******************************************************************************
 * Adul's Smooth Selling Installation
 ******************************************************************************/
// Open UI.MENU
LOG @12
SILENT
LAUNCH_ACTION_MACRO UIMenuOpen
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @20
END

// Patch POPUP_REQUESTER
LOG @179
SILENT
OUTER_SPRINT $UIMenuIF("MenuName") ~POPUP_REQUESTER~
OUTER_SPRINT $UIMenuIF("Body") ~~
LAUNCH_ACTION_MACRO UIMenuGetMenu

ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @180
END

OUTER_SPRINT $UIMenuIF("LuaRegex") ~^[%TAB% ]*action%MWHITE%"%MWHITE%local[%TAB% ]+cnt[%TAB% ]*=[%TAB% ]*tonumber[%TAB% ]*([%TAB% ]*itemRequestAmt[%TAB% ]*)%MWHITE%Infinity_PopMenu[%TAB% ]*([%TAB% ]*)%MWHITE%if[%TAB% ]*([%TAB% ]*cnt[%TAB% ]+and[%TAB% ]+cnt[%TAB% ]*>[%TAB% ]*0[%TAB% ]+and[%TAB% ]+cnt[%TAB% ]*<=[%TAB% ]*requester\.requesterMax[%TAB% ]*)[%TAB% ]*then%MWHITE%requester\.requesterFunc[%TAB% ]*([%TAB% ]*cnt[%TAB% ]*)%MWHITE%end%MWHITE%".*%MEOL%~

OUTER_SPRINT $UIMenuIF("Filespec") ~%PKGNAME%/menu/AdulImprovedStore/PopupRequesterPatch1.menu~

LAUNCH_ACTION_MACRO AdulImprovedStore.PatchBody
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @181
END

LAUNCH_ACTION_MACRO UIMenuUpdateMenu
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @103
END

// Patch STORE_BUYSELL
LOG @122
SILENT
OUTER_SPRINT $UIMenuIF("MenuName") ~STORE_BUYSELL~
OUTER_SPRINT $UIMenuIF("Body") ~~
LAUNCH_ACTION_MACRO UIMenuGetMenu

ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @123
END

OUTER_SPRINT $UIMenuIF("LuaRegex") ~^[%TAB% ]*action%MWHITE%%MDQUOTE%%MNOTDQUOTE%+%MDQUOTE%%MWHITE%actionalt%MWHITE%%MDQUOTE%%MNOTDQUOTE%+%MDQUOTE%%MWHITE%actionDbl%MWHITE%%MDQUOTE%%MWHITE%if[%TAB% ]+not[%TAB% ]+storeScreen:IsCloseBagButtonClickable[%TAB% ]*([%TAB% ]*)[%TAB% ]+or[%TAB% ]+store\.storeItems\[[%TAB% ]*storeItemsVar[%TAB% ]*\]\.valid[%TAB% ]*==[%TAB% ]*1[%TAB% ]+then%MNOTDQUOTE%+%MDQUOTE%.*%MEOL%~

OUTER_SPRINT $UIMenuIF("Filespec") ~%PKGNAME%/menu/AdulImprovedStore/StoreBuySellPatch1.menu~

LAUNCH_ACTION_MACRO AdulImprovedStore.PatchBody
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @124
END

OUTER_SPRINT $UIMenuIF("LuaRegex") ~^[%TAB% ]*action%MWHITE%%MDQUOTE%%MNOTDQUOTE%+%MDQUOTE%%MWHITE%actionalt%MWHITE%%MDQUOTE%%MNOTDQUOTE%+%MDQUOTE%%MWHITE%actionDbl%MWHITE%%MDQUOTE%%MWHITE%if[%TAB% ]*([%TAB% ]*store\.groupItems\[[%TAB% ]*storeGroupItemsVar[%TAB% ]*\]\.item\.isBag[%TAB% ]*==[%TAB% ]*1[%TAB% ]*)[%TAB% ]*then%MNOTDQUOTE%+%MDQUOTE%.*%MEOL%~

OUTER_SPRINT $UIMenuIF("Filespec") ~%PKGNAME%/menu/AdulImprovedStore/StoreBuySellPatch2.menu~

LAUNCH_ACTION_MACRO AdulImprovedStore.PatchBody
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @124
END

OUTER_SPRINT $UIMenuIF("LuaRegex") ~^[%TAB% ]*action%MWHITE%%MDQUOTE%%MNOTDQUOTE%+%MDQUOTE%%MWHITE%actionalt%MWHITE%%MDQUOTE%%MNOTDQUOTE%+%MDQUOTE%%MWHITE%actionDbl%MWHITE%%MDQUOTE%%MWHITE%if[%TAB% ]+store\.groupItems\[[%TAB% ]*storeGroupItemsVar[%TAB% ]*\]\.valid[%TAB% ]*~ ^ "~" ^ ~=[%TAB% ]*0[%TAB% ]*then%MNOTDQUOTE%+%MDQUOTE%.*%MEOL%~

OUTER_SPRINT $UIMenuIF("Filespec") ~%PKGNAME%/menu/AdulImprovedStore/StoreBuySellPatch3.menu~

LAUNCH_ACTION_MACRO AdulImprovedStore.PatchBody
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @124
END

LAUNCH_ACTION_MACRO UIMenuUpdateMenu
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @103
END

// Close UI.MENU
LOG @13
SILENT
LAUNCH_ACTION_MACRO UIMenuClose
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @21
END

