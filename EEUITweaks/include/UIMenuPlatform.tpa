
/*******************************************************************************
 * EEUIGetMenuEnv
 * Sets global variables representing current UI.MENU configuration. Currently
 * defined variables are:
 * 
 * IsBG2 - BG2EE generic.
 * IsBGEE - Base BGEE, no SOD
 * IsSoD  - Base BGEESoD
 * IsBG2onSoD - Kerozevok's BG2EE To BGEE Mod
 * IsSoDOverhaul - Pecca's Dragonspear UI++
 * IsEETBG2  - EET with default BG2 GUI
 * IsEETSoD  - EET with SoD GUI Option
 *
 ******************************************************************************/
DEFINE_ACTION_MACRO EEUIGetMenuEnv
BEGIN
	OUTER_SET IsBG2 = 0
	OUTER_SET IsBGEE = 0
	OUTER_SET IsSoD = 0
	OUTER_SET IsBG2onSoD = 0
	OUTER_SET IsSoDOverhaul = 0
	OUTER_SET IsEETBG2 = 0
	OUTER_SET IsEETSoD = 0

	ACTION_IF GAME_IS ~eet~ THEN
	BEGIN
		ACTION_IF MOD_IS_INSTALLED ~setup-EET_end.tp2~ ~0~ THEN
		BEGIN
			OUTER_SET IsEETSoD = 1
			LOG ~IsEETSoD~
		END ELSE
		BEGIN
			OUTER_SET IsEETBG2 = 1
			LOG ~IsEETBG2~
		END
	END ELSE ACTION_IF GAME_IS ~bg2ee~ THEN
	BEGIN
		OUTER_SET IsBG2 = 1
		LOG ~IsBG2~
	END ELSE 
	BEGIN
		ACTION_IF NOT FILE_EXISTS_IN_GAME ~BD0010.ARE~ THEN
		BEGIN
			OUTER_SET IsBGEE = 1
			LOG ~IsBGEE~
		END ELSE
		BEGIN
			ACTION_IF FILE_EXISTS ~override/ULGOTHST.MOS~ THEN
			BEGIN
				OUTER_SET IsBG2onSoD = 1
				LOG ~IsBG2onSoD~
			END ELSE
			BEGIN
				ACTION_IF FILE_EXISTS_IN_GAME ~rgtitle.png~ THEN
				BEGIN
					OUTER_SET IsSoDOverhaul = 1
					LOG ~IsSoDOverhaul~
				END ELSE
				BEGIN
					OUTER_SET IsSoD = 1
					LOG ~IsSoD~
				END
			END
		END
	END
END
