/*******************************************************************************
 * CustomizePatch
 * 
 * Remove all clickable lua "characterScreen:IsAppearanceButtonClickable();"
 *
 * On completion $UIMenuIF("Succeeded") 1 => Success, 0 => Failure
 * 
 ******************************************************************************/
DEFINE_ACTION_MACRO lefreutCustomAll.CustomizePatch
BEGIN
	LOCAL_SPRINT tempstr $UIMenuIF("Body")
	LOCAL_SET found = 0

	// Put current menu definition into inline file
	COPY - ~ToInlineTemplate~ ~.../CounterInLine~
		REPLACE_EVALUATE ~dummy~ BEGIN END
		~%tempstr%~
	OUTER_SPRINT tempstr ~~

	// Patch
	COPY - ~.../CounterInLine~ ~.../CounterInLine~
		REPLACE_EVALUATE ~^[%TAB% ]*clickable[%TAB% ]+lua%MWHITE%%MQUOTE%%MWHITE%characterScreen:IsAppearanceButtonClickable()%MNOTQUOTE%*%MQUOTE%.*%MEOL%~
		BEGIN
			SET found = 1
		END
		~~

	// Not found - fail
	ACTION_IF NOT %found% THEN
	BEGIN
		LOG ~In lefreutCustomAll.CustomizePatch - failed to find any occurances of clickable lua "characterScreen:IsAppearanceButtonClickable()"~
		OUTER_SET $UIMenuIF("Succeeded") = 0
	END ELSE 

	// Success somewhere - copy .../CounterInLine to $UIMenuIF("Body")
	BEGIN
		OUTER_SPRINT $UIMenuIF("Body") ~~
		OUTER_SET found = SIZE_OF_FILE ~.../CounterInLine~
		COPY - ~.../CounterInLine~ ~.../CounterInLine~
			READ_ASCII 0 $UIMenuIF("Body") (%found%-1)
		OUTER_SET $UIMenuIF("Succeeded") = 1
	END
	COPY - ~EmptyInlineTemplate~ ~.../CounterInline~
END

/*******************************************************************************
 * lefreut's Customize All Characters Installation
 ******************************************************************************/
// Open UI.MENU
PRINT @12
SILENT
LAUNCH_ACTION_MACRO UIMenuOpen
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @20
END

// Patch CHARACTER_CUSTOMIZE menu
PRINT @152
SILENT

OUTER_SPRINT $UIMenuIF("MenuName") ~CHARACTER_CUSTOMIZE~
OUTER_SPRINT $UIMenuIF("Body") ~~
LAUNCH_ACTION_MACRO UIMenuGetMenu
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @153
END

LAUNCH_ACTION_MACRO lefreutCustomAll.CustomizePatch
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @154
END
LAUNCH_ACTION_MACRO UIMenuUpdateMenu
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @103
END

// Close UI.MENU
PRINT @13
SILENT
LAUNCH_ACTION_MACRO UIMenuClose
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @21
END
