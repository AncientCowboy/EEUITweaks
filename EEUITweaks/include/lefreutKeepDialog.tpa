//
// KeepDialog.PatchOnOpen
// Adds 'Infinity_PopMenu('WORLD_MESSAGES')%LE%%TAB%%TAB%Infinity_PushMenu('WORLD_MESSAGES')' to onopen
// On completion $UIMenuIF("Succeeded") 1 => Success, 0 => Failure
//
DEFINE_ACTION_MACRO KeepDialog.PatchOnOpen
BEGIN
	LOCAL_SPRINT tempstr $UIMenuIF("Body")
	LOCAL_SPRINT adder ~Infinity_PopMenu('WORLD_MESSAGES')%LE%%TAB%%TAB%Infinity_PushMenu('WORLD_MESSAGES')~
	LOCAL_SET found = 0

	// Put current menu definition into inline file
	COPY - ~ToInlineTemplate~ ~.../CounterInLine~
		REPLACE_EVALUATE ~dummy~ BEGIN END
		~%tempstr%~

	OUTER_SPRINT tempstr ~~

	// Look for and patch onopen followed by double quoted code block
	COPY - ~.../CounterInLine~ ~.../CounterInLine~
		REPLACE_EVALUATE CASE_INSENSITIVE ~onopen%MWHITE%%MDQUOTE%\(%MNOTDQUOTE%*\)%MDQUOTE%~ 
		BEGIN
			SPRINT tempstr ~onopen%LE%%TAB%"%MATCH1%%LE%%TAB%%TAB%%adder%%LE%%TAB%"~
			SET found = 1
		END
		~%tempstr%~

	// If not found, look for and patch onopen followed by single quoted code block
	ACTION_IF NOT %found% THEN
	BEGIN
		LOG ~In KeepDialog.PatchOnOpen - failed to find onopen with double quotes. Trying single~
		COPY - ~.../CounterInLine~ ~.../CounterInLine~
			REPLACE_EVALUATE CASE_INSENSITIVE ~onopen%MWHITE%%MSQUOTE%\(%MNOTSQUOTE%*\)%MSQUOTE%~
			BEGIN
				SPRINT tempstr ~onopen%LE%%TAB%'%MATCH1%%LE%%TAB%%TAB%%adder%%LE%%TAB%'~
				SET found = 1
			END
			~%tempstr%~
	END

	// If not found, look for modal line and insert our onopen after it
	ACTION_IF NOT %found% THEN
	BEGIN
		LOG ~In KeepDialog.PatchOnOpen - failed to find onopen with single quotes. Trying to add after modal~
		OUTER_SPRINT adder ~%TAB%onopen%LE%%TAB%"%LE%%TAB%%TAB%%adder%%LE%%TAB%"%LE%~
		COPY - ~.../CounterInLine~ ~.../CounterInLine~
			REPLACE_EVALUATE CASE_INSENSITIVE ~modal.*%MEOL%~
			BEGIN
				SPRINT tempstr ~%MATCH0%%adder%~
				SET found = 1
			END
			~%tempstr%~
	END

	// Not found - give up
	ACTION_IF NOT %found% THEN
	BEGIN
		LOG ~In KeepDialog.PatchOnOpen - failed to find modal line~
		OUTER_SET $UIMenuIF("Succeeded") = 0
	END ELSE 
	// Success somewhere - copy .../CounterInLine to $UIMenuIF("Body")
	BEGIN
		OUTER_SPRINT $UIMenuIF("Body") ~~
		OUTER_SET found = SIZE_OF_FILE ~.../CounterInLine~
		COPY - ~.../CounterInLine~ ~.../CounterInLine~
			READ_ASCII 0 $UIMenuIF("Body") (%found%-1)
		OUTER_SET $UIMenuIF("Succeeded") = 1
	END
	COPY - ~EmptyInlineTemplate~ ~.../CounterInline~
END

/*******************************************************************************
 * lefreut's Keep Dialog Box active
 ******************************************************************************/
// Open UI.MENU abstraction
LOG @12
SILENT
LAUNCH_ACTION_MACRO UIMenuOpen
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @20
END

// Patch WORLD_DEATH
LOG @209
SILENT

OUTER_SPRINT $UIMenuIF("MenuName") ~WORLD_DEATH~
OUTER_SPRINT $UIMenuIF("Body") ~~
LAUNCH_ACTION_MACRO UIMenuGetMenu
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @210
END

LAUNCH_ACTION_MACRO KeepDialog.PatchOnOpen
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @211
END

LAUNCH_ACTION_MACRO UIMenuUpdateMenu
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @103
END

// All done - close up
LOG @13
SILENT
LAUNCH_ACTION_MACRO UIMenuClose
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @21
END
