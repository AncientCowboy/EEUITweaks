/*******************************************************************************
 * Mr2150IdentifyItems
 * 
 * Replaces "Infinity_PushMenu('ITEM_IDENTIFY',0,0)" with
 * "
 *	Infinity_OnSpellIdentify(characters[id].equipment[selectedSlot].id);
 *	itemDesc.item = characters[id].equipment[selectedSlot].item
 * " 
 * And optionally:
 *	"C:AddGold(-value_determined_by_cost_choice)"
 *
 * On completion $UIMenuIF("Succeeded") 1 => Success, 0 => Failure
 * 
 ******************************************************************************/
DEFINE_ACTION_MACRO Mr2150IdentifyItems.PatchLuaBlock
BEGIN
	LOCAL_SPRINT tempstr $UIMenuIF("Body")
	LOCAL_SPRINT adder ~~
	LOCAL_SET found = 0

	// Set patch insertion
	ACTION_IF (~%mrautoidgoldcost%~ STRING_EQUAL ~~) THEN
	BEGIN
		OUTER_SET found = SIZE_OF_FILE ~%PKGNAME%/lua/Mr2150IdentifyItems/ItemDescNoCost.lua~
		COPY - ~%PKGNAME%/lua/Mr2150IdentifyItems/ItemDescNoCost.lua~ ~.../CounterInLine~
			READ_ASCII 0 adder (%found%)
		OUTER_SET found = 0 
	END ELSE
	BEGIN
		OUTER_SET found = SIZE_OF_FILE ~%PKGNAME%/lua/Mr2150IdentifyItems/ItemDescWithCost.lua~
		COPY - ~%PKGNAME%/lua/Mr2150IdentifyItems/ItemDescWithCost.lua~ ~.../CounterInLine~
			READ_ASCII 0 adder (%found%)
		OUTER_SET found = 0
		OUTER_SPRINT adder ~%mrautoidgoldcost%~ ^ ~%adder%~
	END

	// Put code block into inline file
	COPY - ~ToInlineTemplate~ ~.../CounterInLine~
		REPLACE_EVALUATE ~dummy~ BEGIN END
		~%tempstr%~
	OUTER_SPRINT tempstr ~~

	// Replace "Infinity_PushMenu('ITEM_IDENTIFY',0,0)"" with new code
	COPY - ~.../CounterInLine~ ~.../CounterInLine~
	REPLACE_EVALUATE ~^[%TAB% ]*Infinity_PushMenu[%TAB% ]*([%TAB% ]*%MQUOTE%ITEM_IDENTIFY%MQUOTE%[%TAB% ]*,[%TAB% ]*0[%TAB% ]*,[%TAB% ]*0[%TAB% ]*).*%MEOL%~
	BEGIN
		SET found = 1
	END
	~%adder%~

	// If not found, nothing to do but fail.
	ACTION_IF NOT %found% THEN
	BEGIN
		LOG ~In Mr2150IdentifyItems.PatchLuaBlock - failed to find target code~
		OUTER_SET $UIMenuIF("Succeeded") = 0
	END

	// Success - copy .../CounterInLine to $UIMenuIF("Body")
	ACTION_IF %found% THEN
	BEGIN
		OUTER_SPRINT $UIMenuIF("Body") ~~
		OUTER_SET found = SIZE_OF_FILE ~.../CounterInLine~
		COPY - ~.../CounterInLine~ ~.../CounterInLine~
			READ_ASCII 0 $UIMenuIF("Body") (%found%-1)
			OUTER_SET $UIMenuIF("Succeeded") = 1
	END
	COPY - ~EmptyInlineTemplate~ ~.../CounterInline~
END

/*******************************************************************************
 * Mr2150's Auto-Identify Items Installation
 ******************************************************************************/
// Open UI.MENU
PRINT @12
SILENT
LAUNCH_ACTION_MACRO UIMenuOpen
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @20
END

// Patch ITEM_DESCRIPTION Lua block
PRINT @304002
SILENT

OUTER_SPRINT $UIMenuIF("LuaRegex") ~function[%TAB% ]+itemDescLeftButtonAction[%TAB% ]*([^)]*)~
OUTER_SPRINT $UIMenuIF("Body") ~~
LAUNCH_ACTION_MACRO UIMenuGetMatchingLuaBlock
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @304003
END

LAUNCH_ACTION_MACRO Mr2150IdentifyItems.PatchLuaBlock
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @304004
END
LAUNCH_ACTION_MACRO UIMenuUpdateLuaBlock
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @103
END

// Close UI.MENU
PRINT @13
SILENT
LAUNCH_ACTION_MACRO UIMenuClose
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @21
END

