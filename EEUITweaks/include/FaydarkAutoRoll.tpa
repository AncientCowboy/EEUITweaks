/*******************************************************************************
 * ChargenRacePatch1
 *
 * Adds '; chargen.selectedRace = chargen.races[ currentChargenRace ].id' 
 * to DONE_BUTTON action
 * 
 * Results in $UIMenuIF("Succeeded") 1 => Success, 0 => Failure
 *
 ******************************************************************************/
DEFINE_ACTION_MACRO FaydarkAutoRoll.ChargenRacePatch1
BEGIN
	LOCAL_SPRINT tempstr $UIMenuIF("Body")
	LOCAL_SET found = 0
	LOCAL_SPRINT adder "; chargen.selectedRace = chargen.races[ currentChargenRace ].id"

	// Put current menu definition into inline file
	COPY - ~ToInlineTemplate~ ~.../CounterInLine~
		REPLACE_EVALUATE ~dummy~ BEGIN END
		~%tempstr%~
	OUTER_SPRINT teempstr ~~

	// Find target, insert code
	COPY - ~.../CounterInLine~ ~.../CounterInLine~
		REPLACE_EVALUATE CASE_INSENSITIVE ~\(^[%TAB% ]*button%MWHITE%{%MNOBRACE%+text[%TAB% ]*%MQUOTE%DONE_BUTTON%MQUOTE%%MNOBRACE%+action%MWHITE%%MDQUOTE%%MNOTDQUOTE%+\)\(%MDQUOTE%%MWHITE%}.*%MEOL%\)~
		BEGIN
			SPRINT tempstr ~%MATCH1%%adder%%MATCH2%~
			SET found = 1
		END
		~%tempstr%~

		PATCH_LOG ~Match1 = "%v1%", Match2 = "%v2%"~
	// Not found, fail
	ACTION_IF NOT %found% THEN
	BEGIN
		LOG ~In FaydarkAutoRoll.ChargenRacePatch1 - failed to find target code~
		OUTER_SET $UIMenuIF("Succeeded") = 0
	END ELSE

	// Otherwise, success. Copy .../CounterInLine to $UIMenuIF("Body")
	ACTION_IF %found% THEN
	BEGIN
		OUTER_SPRINT $UIMenuIF("Body") ~~
		OUTER_SET found = SIZE_OF_FILE ~.../CounterInLine~
		COPY - ~.../CounterInLine~ ~.../CounterInLine~
			READ_ASCII 0 $UIMenuIF("Body") (%found%-1)
		OUTER_SET $UIMenuIF("Succeeded") = 1
	END
	COPY - ~EmptyInlineTemplate~ ~.../CounterInline~
END

/*******************************************************************************
 * ChargenClassPatch1
 *
 * Adds 'chargen.selectedClass = chargen.class[ currentChargenClass ].id' 
 * to DONE_BUTTON action
 * 
 * Results in $UIMenuIF("Succeeded") 1 => Success, 0 => Failure
 *
 ******************************************************************************/
DEFINE_ACTION_MACRO FaydarkAutoRoll.ChargenClassPatch1
BEGIN
	LOCAL_SPRINT tempstr $UIMenuIF("Body")
	LOCAL_SET found = 0
	LOCAL_SPRINT adder "chargen.selectedClass = chargen.class[ currentChargenClass ].id"

	// Put current menu definition into inline file
	COPY - ~ToInlineTemplate~ ~.../CounterInLine~
		REPLACE_EVALUATE ~dummy~ BEGIN END
		~%tempstr%~
	OUTER_SPRINT teempstr ~~

	// Find target, insert code
	COPY - ~.../CounterInLine~ ~.../CounterInLine~
		REPLACE_EVALUATE CASE_INSENSITIVE ~\(^[%TAB% ]*button%MWHITE%{%MNOBRACE%+text[%TAB% ]*%MQUOTE%DONE_BUTTON%MQUOTE%%MNOBRACE%+action%MWHITE%%MDQUOTE%%MNOTDQUOTE%+\)\(%MDQUOTE%%MWHITE%}.*%MEOL%\)~
		BEGIN
			SPRINT tempstr ~%MATCH1%%adder%%MATCH2%~
			SET found = 1
		END
		~%tempstr%~

	// Not found, fail
	ACTION_IF NOT %found% THEN
	BEGIN
		LOG ~In FaydarkAutoRoll.ChargenClassPatch1 - failed to find target code~
		OUTER_SET $UIMenuIF("Succeeded") = 0
	END ELSE

	// Otherwise, success. Copy .../CounterInLine to $UIMenuIF("Body")
	ACTION_IF %found% THEN
	BEGIN
		OUTER_SPRINT $UIMenuIF("Body") ~~
		OUTER_SET found = SIZE_OF_FILE ~.../CounterInLine~
		COPY - ~.../CounterInLine~ ~.../CounterInLine~
			READ_ASCII 0 $UIMenuIF("Body") (%found%-1)
		OUTER_SET $UIMenuIF("Succeeded") = 1
	END
	COPY - ~EmptyInlineTemplate~ ~.../CounterInline~
END

/*******************************************************************************
 * Faydark's Abilities Auto-Roller Installation
 ******************************************************************************/
// Open UI.MENU
LOG @12
SILENT
LAUNCH_ACTION_MACRO UIMenuOpen
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @20
END

// Patch CHARGEN_RACE menu
LOG @161
SILENT

OUTER_SPRINT $UIMenuIF("MenuName") ~CHARGEN_RACE~
OUTER_SPRINT $UIMenuIF("Body") ~~
LAUNCH_ACTION_MACRO UIMenuGetMenu
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @162
END

LAUNCH_ACTION_MACRO FaydarkAutoRoll.ChargenRacePatch1
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @163
END

LAUNCH_ACTION_MACRO UIMenuUpdateMenu
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @103
END

// Patch CHARGEN_CLASS menu
LOG @164
SILENT

OUTER_SPRINT $UIMenuIF("MenuName") ~CHARGEN_CLASS~
OUTER_SPRINT $UIMenuIF("Body") ~~
LAUNCH_ACTION_MACRO UIMenuGetMenu
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @165
END

LAUNCH_ACTION_MACRO FaydarkAutoRoll.ChargenClassPatch1
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @166
END

LAUNCH_ACTION_MACRO UIMenuUpdateMenu
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @103
END

// Replace CHARGEN_ABILITIES menu
LOG @107001
SILENT
OUTER_SPRINT $UIMenuIF("MenuName") ~CHARGEN_ABILITIES~

ACTION_MATCH 1
WITH
	%IsSoD% %IsEETSoD% %IsKRDS%
	BEGIN
		OUTER_SPRINT $UIMenuIF("Filespec") ~%PKGNAME%/menu/FaydarkAutoRoll/chargenabilitiesmenu1.menu~
	END
	%IsBGEE%
	BEGIN
		OUTER_SPRINT $UIMenuIF("Filespec") ~%PKGNAME%/menu/FaydarkAutoRoll/chargenabilitiesmenu2.menu~
	END
	%IsBG2% %IsBG2onSoD% %IsEETBG2% %IsTIWDonBG2% %IsTIWDonSoD% %IsTIWD2onBG2%
	BEGIN
		OUTER_SPRINT $UIMenuIF("Filespec") ~%PKGNAME%/menu/FaydarkAutoRoll/chargenabilitiesmenu3.menu~
	END		
	%IsLeUIBG2% %IsLeUISOD% %IsLeUIBG1% %IsEETLeUI%
	BEGIN
		OUTER_SPRINT $UIMenuIF("Filespec") ~%PKGNAME%/menu/FaydarkAutoRoll/chargenabilitiesmenu4.menu~
	END
		
	DEFAULT
		FAIL @15
END

LAUNCH_ACTION_MACRO UIMenuReplaceMenuMixed
ACTION_IF NOT $UIMenuIF("Succeeded") THEN 
BEGIN
	FAIL @107002
END

// Close up
LOG @13
SILENT
LAUNCH_ACTION_MACRO UIMenuClose
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @21
END

// Copying ui specific files
LOG @17
SILENT
ACTION_MATCH 1
WITH
	%IsLeUISOD% %IsLeUIBG1%
	BEGIN
		COPY ~%PKGNAME%/copy/FayDarkAutoRoll~ ~override~
	END

	DEFAULT

END




