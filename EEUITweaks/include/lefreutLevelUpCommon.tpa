/*******************************************************************************
 * updateAttrTableLuaPatch
 * 
 * Adds if (characters[currentID] == nil) then return end to beginning of
 * function.
 * 
 * On completion $UIMenuIF("Succeeded") 1 => Success, 0 => Failure
 *
 ******************************************************************************/
DEFINE_ACTION_MACRO lefreutLevelUpCommon.updateAttrTableLuaPatch
BEGIN
	LOCAL_SPRINT	adder ~%TAB%if (characters[currentID] == nil) then%LE%%TAB%%TAB%return%LE%%TAB%end%LE%~
	LOCAL_SET		found = 0
	LOCAL_SPRINT	tempstr ~~

	// Put the block into inline file
	OUTER_SPRINT tempstr $UIMenuIF("Body")
	COPY - ~EmptyInlineTemplate~ ~.../CounterInline~

	COPY - ~ToInlineTemplate~ ~.../CounterInLine~
		REPLACE_EVALUATE ~dummy~ BEGIN END
		~%tempstr%~
	
	// Patch the function
	OUTER_SET found = 0
	OUTER_SPRINT tempstr ~~
	COPY - ~.../CounterInLine~ ~.../CounterInLine~
		REPLACE_EVALUATE ~^[%TAB% ]*function[%TAB% ]+updateAttrTable().*%MEOL%~
		BEGIN
			SPRINT tempstr ~%MATCH0%%adder%~
			SET found = 1
		END
		~%tempstr%~

	ACTION_IF %found% THEN
	BEGIN
		OUTER_SPRINT $UIMenuIF("Body") ~~
		OUTER_SET found = SIZE_OF_FILE ~.../CounterInLine~
		COPY - ~.../CounterInLine~ ~.../CounterInLine~
			READ_ASCII 0 $UIMenuIF("Body") (%found%-1)
		OUTER_SET $UIMenu("Succeeded") = 1
	END ELSE

	BEGIN// Didn't find target insertion point
		LOG ~In lefreutLevelUpCommon.updateAttrTableLuaPatch: Didn't find insertion point for new code~
		OUTER_SET $UIMenu("Succeeded") = 0
	END
	COPY - ~EmptyInlineTemplate~ ~.../CounterInline~

END

/*******************************************************************************
 * CommonInstall
 * 
 * Common installation code for all versions. Called with filename in
 * $UIMenuIF("Filespec")
 *
 * Deals with failure locally
 ******************************************************************************/
DEFINE_ACTION_MACRO lefreutLevelUpCommon.CommonInstall 
BEGIN
	LOCAL_SPRINT savedfspec $UIMenuIF("Filespec")

	// Open UI.MENU
	PRINT @12
	SILENT
	LAUNCH_ACTION_MACRO UIMenuOpen
	ACTION_IF NOT $UIMenuIF("Succeeded") THEN
	BEGIN
		FAIL @20
	END

	// Replace WORLD_LEVEL_UP_BUTTONS menu
	PRINT @204404
	SILENT

	OUTER_SPRINT $UIMenuIF("MenuName") ~WORLD_LEVEL_UP_BUTTONS~
	OUTER_SPRINT $UIMenuIF("Filespec") ~%savedfspec%~
	LAUNCH_ACTION_MACRO UIMenuReplaceMenu
	ACTION_IF NOT $UIMenuIF("Succeeded") THEN
	BEGIN
		FAIL @204404
	END

	// Patch updateAttrTable function
	PRINT @204401
	SILENT

	OUTER_SPRINT $UIMenuIF("LuaRegex") ~function updateAttrTable()~
	OUTER_SPRINT $UIMenuIF("Body") ~~
	OUTER_SET $UIMenuIF("LuaID")  = 0 - 1
	LAUNCH_ACTION_MACRO UIMenuGetMatchingLuaBlock
	ACTION_IF NOT $UIMenuIF("Succeeded") THEN
	BEGIN
		FAIL @204402
	END
	LAUNCH_ACTION_MACRO lefreutLevelUpCommon.updateAttrTableLuaPatch
	ACTION_IF NOT $UIMenuIF("Succeeded") THEN 
	BEGIN
		FAIL @204403
	END

	LAUNCH_ACTION_MACRO UIMenuUpdateLuaBlock
	ACTION_IF NOT $UIMenuIF("Succeeded") THEN
	BEGIN
		FAIL @103
	END

	// All done - close up
	PRINT @13
	SILENT
	LAUNCH_ACTION_MACRO UIMenuClose
	ACTION_IF NOT $UIMenuIF("Succeeded") THEN
	BEGIN
		FAIL @21
	END
END	

/*******************************************************************************
 * InstallButtons
 * 
 * Installs lefreut's Level Up Buttons version of lefreut's Better Level Up 
 * Indicators
 *
 ******************************************************************************/
DEFINE_ACTION_MACRO lefreutLevelUpCommon.InstallButtons 
BEGIN

	OUTER_SPRINT $UIMenuIF("Filespec") ~%PKGNAME%/menu/lefreutLU/wlumenubuttons.menu~
	LAUNCH_ACTION_MACRO lefreutLevelUpCommon.CommonInstall

END	

/*******************************************************************************
 * InstallGlowWithClick
 * 
 * lefreut's Level Up Glow with Click version of lefreut's Better Level Up 
 * Indicators
 *
 ******************************************************************************/

DEFINE_ACTION_MACRO lefreutLevelUpCommon.InstallGlowWithClick 
BEGIN

	OUTER_SPRINT $UIMenuIF("Filespec") ~%PKGNAME%/menu/lefreutLU/wlumenuglowclick.menu~
	LAUNCH_ACTION_MACRO lefreutLevelUpCommon.CommonInstall

END

/*******************************************************************************
 * InstallGlowNoClick
 * 
 * lefreut's Level Up Glow no Click version of lefreut's Better Level Up 
 * Indicators
 *
 * Deals with failure locally
 ******************************************************************************/
DEFINE_ACTION_MACRO lefreutLevelUpCommon.InstallGlowNoClick 
BEGIN

	OUTER_SPRINT $UIMenuIF("Filespec") ~%PKGNAME%/menu/lefreutLU/wlumenuglownoclick.menu~
	LAUNCH_ACTION_MACRO lefreutLevelUpCommon.CommonInstall

END	


