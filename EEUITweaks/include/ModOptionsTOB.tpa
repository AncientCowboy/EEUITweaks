/*******************************************************************************
 * PatchStartOptions
 * Inserts button defined in 'modostartopt1.menu' as the last element of
 * START_OPTIONS menu.
 * On completion $UIMenuIF("Succeeded") 1 => Success, 0 => Failure
 ******************************************************************************/
DEFINE_ACTION_MACRO ModOptionsTOB.PatchStartOptions
BEGIN
	LOCAL_SPRINT tempstr $UIMenuIF("Body")
	LOCAL_SPRINT adder ~~

	// Read patch insertion from modostartopt1.menu
	LOCAL_SET found = SIZE_OF_FILE ~%PKGNAME%/menu/ModsOptions/modostartopt1.menu~
	COPY - ~%PKGNAME%/menu/ModsOptions/modostartopt1.menu~ ~.../CounterInLine~
		READ_ASCII 0 adder (%found%)
	OUTER_SET found = 0 

	// Put current menu definition into inline file
	COPY - ~ToInlineTemplate~ ~.../CounterInLine~
		REPLACE_EVALUATE ~dummy~ BEGIN END
		~%tempstr%~
	OUTER_SPRINT tempstr ~~

	// First check if the button has already been manually patched in
	COPY - ~.../CounterInLine~ ~.../CounterInLine~
		REPLACE_EVALUATE ~MODOPT_BUTTON~
		BEGIN
			SET found = 1
		END
		~%MATCH0%~

	// If not, search for all of menu except closing brace, insert new button code
	ACTION_IF NOT %found% THEN
	BEGIN
		COPY - ~.../CounterInLine~ ~.../CounterInLine~
			REPLACE_EVALUATE CASE_INSENSITIVE ~menu%MWHITE%{\(%MNOBRACE%*%MBRACES%\)+%MNOBRACE%*~
			BEGIN
				SPRINT tempstr ~%MATCH0%%adder%~
				SET found = 1
			END
			~%tempstr%~
	END

	// If not found, nothing to do but fail.
	ACTION_IF NOT %found% THEN
	BEGIN
		LOG ~In ModOptionsTOB.PatchStartOptions - failed to match menu body - bailing out~
		OUTER_SET $UIMenuIF("Succeeded") = 0
	END ELSE 

	// Success copy .../CounterInLine to $UIMenuIF("Body")
	BEGIN
		OUTER_SPRINT $UIMenuIF("Body") ~~
		OUTER_SET found = SIZE_OF_FILE ~.../CounterInLine~
		COPY - ~.../CounterInLine~ ~.../CounterInLine~
			READ_ASCII 0 $UIMenuIF("Body") (%found%-1)
		OUTER_SET $UIMenuIF("Succeeded") = 1
	END
	COPY - ~EmptyInlineTemplate~ ~.../CounterInline~
END

/*******************************************************************************
 * EscMenuFirstPatch
 * Adds enabled criteria to the "OPTIONS_TITLE" label
 * On completion $UIMenuIF("Succeeded") 1 => Success, 0 => Failure
 ******************************************************************************/
DEFINE_ACTION_MACRO ModOptionsTOB.EscMenuFirstPatch
BEGIN
	LOCAL_SPRINT tempstr $UIMenuIF("Body")
	LOCAL_SPRINT adder ~%LE%%TAB%%TAB%enabled "(not(modOptionsIngameDisabled) and 1) or modOptionsIngameDisabled()"~
	LOCAL_SET found = 0

	// Put current menu definition into inline file
	COPY - ~ToInlineTemplate~ ~.../CounterInLine~
		REPLACE_EVALUATE ~dummy~ BEGIN END
		~%tempstr%~
	OUTER_SPRINT tempstr ~~

	// First, check to see if the patch has already been manually applied.
	COPY - ~.../CounterInLine~ ~.../CounterInLine~
		REPLACE_EVALUATE ~([%TAB% ]*not[%TAB% ]*([%TAB% ]*modOptionsIngameDisabled[%TAB% ]*)%MNOBRACE%*OPTIONS_TITLE~
		BEGIN
			SET found = 1
		END
		~%MATCH0%~

	// If not found, look for and patch the 'text "OPTIONS_TITLE"' label
	ACTION_IF NOT %found% THEN
	BEGIN
		COPY - ~.../CounterInLine~ ~.../CounterInLine~
			REPLACE_EVALUATE CASE_INSENSITIVE ~\(label%MWHITE%{\)\(%MWHITE%area[%TAB% ]+[0-9]+[%TAB% ]+[0-9]+[%TAB% ]+[0-9]+[%TAB% ][0-9]+%MWHITE%text[%TAB% ]+%MQUOTE%[%TAB% ]*OPTIONS_TITLE[%TAB% ]*%MQUOTE%\)~
			BEGIN
				SPRINT tempstr ~%MATCH1%%adder%%MATCH2%~
				SET found = 1
			END
			~%tempstr%~
	END

	// Not found - give up
	ACTION_IF NOT %found% THEN
	BEGIN
		LOG ~In ModOptionsTOB.EscMenuFirstPatch - failed to find OPTIONS_TITLE label - bailing out~
		OUTER_SET $UIMenuIF("Succeeded") = 0
	END ELSE 

	// Success - copy .../CounterInLine to $UIMenuIF("Body")
	BEGIN
		OUTER_SPRINT $UIMenuIF("Body") ~~
		OUTER_SET found = SIZE_OF_FILE ~.../CounterInLine~
		COPY - ~.../CounterInLine~ ~.../CounterInLine~
			READ_ASCII 0 $UIMenuIF("Body") (%found%-1)
		OUTER_SET $UIMenuIF("Succeeded") = 1
	END
	COPY - ~EmptyInlineTemplate~ ~.../CounterInline~
END

/*******************************************************************************
 * EscMenuSecondPatch
 * Adds enabled criteria to the "versionString" label
 * On completion $UIMenuIF("Succeeded") 1 => Success, 0 => Failure
 ******************************************************************************/
DEFINE_ACTION_MACRO ModOptionsTOB.EscMenuSecondPatch
BEGIN
	LOCAL_SPRINT tempstr $UIMenuIF("Body")
	LOCAL_SPRINT adder ~%LE%%TAB%%TAB%enabled "(not(modOptionsIngameDisabled) and 1) or modOptionsIngameDisabled()"~
	LOCAL_SET found = 0

	// Put current menu definition into inline file
	COPY - ~ToInlineTemplate~ ~.../CounterInLine~
		REPLACE_EVALUATE ~dummy~ BEGIN END
		~%tempstr%~
	OUTER_SPRINT tempstr ~~

	// First, check to see if the patch has already been manually applied.
	COPY - ~.../CounterInLine~ ~.../CounterInLine~
		REPLACE_EVALUATE ~([%TAB% ]*not[%TAB% ]*([%TAB% ]*modOptionsIngameDisabled[%TAB% ]*)%MNOBRACE%*versionString~
		BEGIN
			SET found = 1
		END
		~%MATCH0%~

	// If not found, look for and patch the 'text lua "versionString"' label
	ACTION_IF NOT %found% THEN
	BEGIN
		COPY - ~.../CounterInLine~ ~.../CounterInLine~
			REPLACE_EVALUATE CASE_INSENSITIVE ~\(label%MWHITE%{\)\(%MWHITE%area[%TAB% ]+[0-9]+[%TAB% ]+[0-9]+[%TAB% ]+[0-9]+[%TAB% ][0-9]+%MWHITE%text[%TAB% ]+lua[%TAB% ]+%MQUOTE%[%TAB% ]*versionString[%TAB% ]*%MQUOTE%\)~
			BEGIN
				SPRINT tempstr ~%MATCH1%%adder%%MATCH2%~
				SET found = 1
			END
			~%tempstr%~
	END

	// Not found - give up
	ACTION_IF NOT %found% THEN
	BEGIN
		LOG ~In ModOptionsTOB.EscMenuSecondPatch - failed to find versionString label - bailing out~
		OUTER_SET $UIMenuIF("Succeeded") = 0
	END ELSE

	// Success - copy .../CounterInLine to $UIMenuIF("Body")
	BEGIN
		OUTER_SPRINT $UIMenuIF("Body") ~~
		OUTER_SET found = SIZE_OF_FILE ~.../CounterInLine~
		COPY - ~.../CounterInLine~ ~.../CounterInLine~
			READ_ASCII 0 $UIMenuIF("Body") (%found%-1)
		OUTER_SET $UIMenuIF("Succeeded") = 1
	END
	COPY - ~EmptyInlineTemplate~ ~.../CounterInline~
END

/*******************************************************************************
 * EscMenuThirdPatch
 * Inserts button defined in modoesc2.menu before the GAMEPLAY_BUTTON button
 * On completion $UIMenuIF("Succeeded") 1 => Success, 0 => Failure
 ******************************************************************************/
DEFINE_ACTION_MACRO ModOptionsTOB.EscMenuThirdPatch
BEGIN
	LOCAL_SPRINT tempstr $UIMenuIF("Body")
	LOCAL_SPRINT adder ~~

	// Read patch insertion from modoesc2.menu
	LOCAL_SET found = SIZE_OF_FILE ~%PKGNAME%/menu/ModsOptions/modoesc2.menu~
	COPY - ~%PKGNAME%/menu/ModsOptions/modoesc2.menu~ ~.../CounterInLine~
		READ_ASCII 0 adder (%found%)
	OUTER_SET found = 0 

	// Put current menu definition into inline file
	COPY - ~ToInlineTemplate~ ~.../CounterInLine~
		REPLACE_EVALUATE ~dummy~ BEGIN END
		~%tempstr%~
	OUTER_SPRINT tempstr ~~

	// First check if the button has already been manually patched in
	COPY - ~.../CounterInLine~ ~.../CounterInLine~
		REPLACE_EVALUATE ~Infinity_PushMenu[%TAB% ]*([%TAB% ]*%MQUOTE%[%TAB% ]*OPTIONS_MODS[%TAB% ]*%MQUOTE%[%TAB% ]*)~
		BEGIN
			SET found = 1
		END
		~%MATCH0%~

	// If not, search for GAMEPLAY_BUTTON, insert new button code before
	ACTION_IF NOT %found% THEN
	BEGIN
		COPY - ~.../CounterInLine~ ~.../CounterInLine~
			REPLACE_EVALUATE CASE_INSENSITIVE ~[%TAB% ]+button%MWHITE%{%MNOBRACE%+Infinity_PushMenu[%TAB% ]*([%TAB ]*%MQUOTE%OPTIONS_GAMEPLAY%MQUOTE%[%TAB% ])~
			BEGIN
				SPRINT tempstr ~%adder%%MATCH0%~
				SET found = 1
			END
			~%tempstr%~
	END

	// Not found - give up
	ACTION_IF NOT %found% THEN
	BEGIN
		LOG ~In ModOptionsTOB.EscMenuThirdPatch - failed to find GAMEPLAY_BUTTON - bailing out~
		OUTER_SET $UIMenuIF("Succeeded") = 0
	END ELSE 

	// Success - copy .../CounterInLine to $UIMenuIF("Body")
	BEGIN
		OUTER_SPRINT $UIMenuIF("Body") ~~
		OUTER_SET found = SIZE_OF_FILE ~.../CounterInLine~
		COPY - ~.../CounterInLine~ ~.../CounterInLine~
			READ_ASCII 0 $UIMenuIF("Body") (%found%-1)
		OUTER_SET $UIMenuIF("Succeeded") = 1
	END
	COPY - ~EmptyInlineTemplate~ ~.../CounterInline~

END

/*******************************************************************************
 * Mods Options Installation
 ******************************************************************************/

PRINT @24
SILENT
COPY ~%PKGNAME%/copy/ModsOptions/M_ModsOp.lua~ ~override~
COPY ~%PKGNAME%/copy/ModsOptions/en_ModOp.lua~ ~override~
COPY ~%PKGNAME%/copy/ModsOptions/modoimg1.BAM~ ~override~

// Open UI.MENU abstraction
PRINT @12
SILENT
LAUNCH_ACTION_MACRO UIMenuOpen
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @20
END

// Patch START_OPTIONS menu
PRINT @100
SILENT

OUTER_SPRINT $UIMenuIF("MenuName") ~START_OPTIONS~
OUTER_SPRINT $UIMenuIF("Body") ~~
LAUNCH_ACTION_MACRO UIMenuGetMenu
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @101
END

LAUNCH_ACTION_MACRO ModOptionsTOB.PatchStartOptions
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @102
END

LAUNCH_ACTION_MACRO UIMenuUpdateMenu
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @103
END

// Patch ESC_MENU
PRINT @104
SILENT

OUTER_SPRINT $UIMenuIF("MenuName") ~ESC_MENU~
OUTER_SPRINT $UIMenuIF("Body") ~~
LAUNCH_ACTION_MACRO UIMenuGetMenu
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @105
END

LAUNCH_ACTION_MACRO ModOptionsTOB.EscMenuFirstPatch
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @106
END

LAUNCH_ACTION_MACRO ModOptionsTOB.EscMenuSecondPatch
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @106
END

LAUNCH_ACTION_MACRO ModOptionsTOB.EscMenuThirdPatch
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @106
END

LAUNCH_ACTION_MACRO UIMenuUpdateMenu
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @103
END

// Add ModsOptions menus
	PRINT @100001
SILENT

OUTER_SPRINT $UIMenuIF("MenuName") ~OPTIONS_MODS~
OUTER_SPRINT $UIMenuIF("Body") ~~
LAUNCH_ACTION_MACRO UIMenuGetMenu
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN // Install main menu and options menu
	OUTER_SPRINT $UIMenuIF("Filespec") ~%PKGNAME%/menu/ModsOptions/modoptions3.menu~
	LAUNCH_ACTION_MACRO UIMenuAppendFile
	ACTION_IF NOT $UIMenuIF("Succeeded") THEN
	BEGIN
		FAIL @100002
	END
END ELSE 
BEGIN // Old package installed, just patch main menu
	OUTER_SPRINT $UIMenuIF("Filespec") ~%PKGNAME%/menu/ModsOptions/modooptmod1.menu~
	LAUNCH_ACTION_MACRO UIMenuReplaceMenu
	ACTION_IF NOT $UIMenuIF("Succeeded") THEN
	BEGIN
		FAIL @100002
	END
END

// Done - close UI.MENU
PRINT @13
SILENT
LAUNCH_ACTION_MACRO UIMenuClose
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @21
END

