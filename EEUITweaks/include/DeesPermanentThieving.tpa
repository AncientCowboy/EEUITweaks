
/*******************************************************************************
 * ActionBarPatch1
 *
 * Replaces contents of ACTION_BAR label
 *
 * Returns $UIMenuIF("Succeeded") 1 => Success, 0 => Failure
 *
 ******************************************************************************/
DEFINE_ACTION_MACRO DeesPermanent.ActionBarPatch1
BEGIN
	LOCAL_SPRINT tempstr $UIMenuIF("Body")
	LOCAL_SPRINT adder ~~
	LOCAL_SPRINT filestr $UIMenuIF("Filespec")

	// Read patch insertion from onopeninsert.lua
	LOCAL_SET found = SIZE_OF_FILE ~%filestr%~
	COPY - ~%filestr%~ ~.../CounterInLine~
		READ_ASCII 0 adder (%found%)
	OUTER_SET found = 0 

	// Put code block into inline file
	COPY - ~ToInlineTemplate~ ~.../CounterInLine~
		REPLACE_EVALUATE ~dummy~ BEGIN END
		~%tempstr%~
	OUTER_SPRINT tempstr ~~

	// Replace label definition
	COPY - ~.../CounterInLine~ ~.../CounterInLine~
		REPLACE_EVALUATE CASE_INSENSITIVE ~^[%TAB% ]*label%MWHITE%{%MWHITE%area%MNOBRACE%+mosaic%MNOBRACE%+}.*%MEOL%~
		BEGIN
			SET found = 1
		END
		~%adder%~

	// Not found - give up
	ACTION_IF NOT %found% THEN
	BEGIN
		LOG ~In DeesPermanent.ActionBarPatch1 - failed to find label definition~
		OUTER_SET $UIMenuIF("Succeeded") = 0
	END ELSE 

	// Success somewhere - copy .../CounterInLine to $UIMenuIF("Body")
	BEGIN
		OUTER_SPRINT $UIMenuIF("Body") ~~
		OUTER_SET found = SIZE_OF_FILE ~.../CounterInLine~
		COPY - ~.../CounterInLine~ ~.../CounterInLine~
			READ_ASCII 0 $UIMenuIF("Body") (%found%-1)
		OUTER_SET $UIMenuIF("Succeeded") = 1
	END
	COPY - ~EmptyInlineTemplate~ ~.../CounterInline~
END

/*******************************************************************************
 * ActionBarPatch2
 *
 * Replaces existing ACTION_BAR Message scroll buttons
 *
 * Returns $UIMenuIF("Succeeded") 1 => Success, 0 => Failure
 *
 ******************************************************************************/
DEFINE_ACTION_MACRO DeesPermanent.ActionBarPatch2
BEGIN
	LOCAL_SPRINT tempstr $UIMenuIF("Body")
	LOCAL_SPRINT adder ~~
	LOCAL_SPRINT filestr $UIMenuIF("Filespec")

	// Read patch insertion from onopeninsert.lua
	LOCAL_SET found = SIZE_OF_FILE ~%filestr%~
	COPY - ~%filestr%~ ~.../CounterInLine~
		READ_ASCII 0 adder (%found%)
	OUTER_SET found = 0 

	// Put code block into inline file
	COPY - ~ToInlineTemplate~ ~.../CounterInLine~
		REPLACE_EVALUATE ~dummy~ BEGIN END
		~%tempstr%~
	OUTER_SPRINT tempstr ~~

	// Replace scroll button definitions
	COPY - ~.../CounterInLine~ ~.../CounterInLine~
		REPLACE_EVALUATE CASE_INSENSITIVE ~^[%TAB% ]*button%MWHITE%{%MNOBRACE%+dragMessages%MNOBRACE%+}%MWHITE%button%MWHITE%{%MNOBRACE%+dragMessages%MNOBRACE%+}.*%MEOL%~
		BEGIN
			SET found = 1
		END
		~%adder%~

	// Not found - give up
	ACTION_IF NOT %found% THEN
	BEGIN
		LOG ~In DeesPermanent.ActionBarPatch2 - failed to find scroll buttons~
		OUTER_SET $UIMenuIF("Succeeded") = 0
	END ELSE 

	// Success somewhere - copy .../CounterInLine to $UIMenuIF("Body")
	BEGIN
		OUTER_SPRINT $UIMenuIF("Body") ~~
		OUTER_SET found = SIZE_OF_FILE ~.../CounterInLine~
		COPY - ~.../CounterInLine~ ~.../CounterInLine~
			READ_ASCII 0 $UIMenuIF("Body") (%found%-1)
		OUTER_SET $UIMenuIF("Succeeded") = 1
	END
	COPY - ~EmptyInlineTemplate~ ~.../CounterInline~
END

/*******************************************************************************
 * Dee's Permanent Thieving Button Installation
 ******************************************************************************/	
// Open UI.MENU
LOG @12
SILENT
LAUNCH_ACTION_MACRO UIMenuOpen
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @20
END

// Patch WORLD_ACTIONBAR
LOG @119
SILENT
OUTER_SPRINT $UIMenuIF("MenuName") ~WORLD_ACTIONBAR~

// For Kilivitz's Revised Dragon Scale v2 this is a menu replacement rather than patch
ACTION_IF IsKRDS THEN
BEGIN
	ACTION_IF MOD_IS_INSTALLED ~EEUITweaks.tp2~ ~2010~ THEN
	BEGIN
		OUTER_SPRINT $UIMenuIF("Filespec") ~%PKGNAME%/menu/DeePermanentThieving/ABarFull7Large.menu~
	END ELSE
	BEGIN
		OUTER_SPRINT $UIMenuIF("Filespec") ~%PKGNAME%/menu/DeePermanentThieving/ABarFull7Medium.menu~
	END

	LAUNCH_ACTION_MACRO UIMenuReplaceMenu
	ACTION_IF NOT $UIMenuIF("Succeeded") THEN
	BEGIN
		FAIL @121
	END

END ELSE

// Everyone else
BEGIN
	OUTER_SPRINT $UIMenuIF("Body") ~~
	LAUNCH_ACTION_MACRO UIMenuGetMenu
	ACTION_IF NOT $UIMenuIF("Succeeded") THEN
	BEGIN
		FAIL @120
	END
	ACTION_MATCH 1
	WITH
		%IsSoD% %IsEETSoD% %IsTIWDonSoD% %IsKRDS0% %IsKRDS1% %IsKRDS2% %IsKRDS3% %IsKRDS4% %IsKRDS5%
		BEGIN
			ACTION_IF MOD_IS_INSTALLED ~EEUITweaks.tp2~ ~2010~ THEN
			BEGIN
				OUTER_SPRINT $UIMenuIF("Filespec") ~%PKGNAME%/menu/DeePermanentThieving/ABarLabel1Large.menu~
			END ELSE
			BEGIN
				OUTER_SPRINT $UIMenuIF("Filespec") ~%PKGNAME%/menu/DeePermanentThieving/ABarLabel1Medium.menu~
			END
		END
		%IsSoDOverhaul%
		BEGIN
			ACTION_IF MOD_IS_INSTALLED ~EEUITweaks.tp2~ ~2010~ THEN
			BEGIN
				OUTER_SPRINT $UIMenuIF("Filespec") ~%PKGNAME%/menu/DeePermanentThieving/ABarLabel2Large.menu~
			END ELSE
			BEGIN
				OUTER_SPRINT $UIMenuIF("Filespec") ~%PKGNAME%/menu/DeePermanentThieving/ABarLabel2Medium.menu~
			END
		END
		%IsBG2% %IsEETBG2% %IsTIWDonBG2% %IsTIWD2onBG2%
		BEGIN
			ACTION_IF MOD_IS_INSTALLED ~EEUITweaks.tp2~ ~2010~ THEN
			BEGIN
				OUTER_SPRINT $UIMenuIF("Filespec") ~%PKGNAME%/menu/DeePermanentThieving/ABarLabel3Large.menu~
			END ELSE
			BEGIN
				OUTER_SPRINT $UIMenuIF("Filespec") ~%PKGNAME%/menu/DeePermanentThieving/ABarLabel3Medium.menu~
			END
		END
		%IsBGEE%
		BEGIN
			ACTION_IF MOD_IS_INSTALLED ~EEUITweaks.tp2~ ~2010~ THEN
			BEGIN
				OUTER_SPRINT $UIMenuIF("Filespec") ~%PKGNAME%/menu/DeePermanentThieving/ABarLabel4Large.menu~
			END ELSE
			BEGIN
				OUTER_SPRINT $UIMenuIF("Filespec") ~%PKGNAME%/menu/DeePermanentThieving/ABarLabel4Medium.menu~
			END
		END
		%IsBG2onSoD%
		BEGIN
			OUTER_SPRINT $UIMenuIF("Filespec") ~%PKGNAME%/menu/DeePermanentThieving/ABarLabel5Medium.menu~
		END
		%IsLeUIBG2% %IsLeUISOD% %IsLeUIBG1% %IsEETLeUI%
		BEGIN
			ACTION_IF MOD_IS_INSTALLED ~EEUITweaks.tp2~ ~2010~ THEN
			BEGIN
				OUTER_SPRINT $UIMenuIF("Filespec") ~%PKGNAME%/menu/DeePermanentThieving/ABarLabel6Large.menu~
			END ELSE
			BEGIN
				OUTER_SPRINT $UIMenuIF("Filespec") ~%PKGNAME%/menu/DeePermanentThieving/ABarLabel6Medium.menu~
			END
		END
	
		DEFAULT
			FAIL @15
	END
	LAUNCH_ACTION_MACRO DeesPermanent.ActionBarPatch1
	ACTION_IF NOT $UIMenuIF("Succeeded") THEN
	BEGIN
		FAIL @121
	END
	LAUNCH_ACTION_MACRO UIMenuUpdateMenu
	ACTION_IF NOT $UIMenuIF("Succeeded") THEN
	BEGIN
		FAIL @103
	END
	
	ACTION_MATCH 1
	WITH
		%IsSoD% %IsEETSoD% %IsTIWDonSoD% %IsKRDS0% %IsKRDS1% %IsKRDS2% %IsKRDS3% %IsKRDS4% %IsKRDS5%
		BEGIN
			OUTER_SPRINT $UIMenuIF("Filespec") ~%PKGNAME%/menu/DeePermanentThieving/ActionBarButton1.menu~
		END
		%IsSoDOverhaul%
		BEGIN
			OUTER_SPRINT $UIMenuIF("Filespec") ~%PKGNAME%/menu/DeePermanentThieving/ActionBarButton2.menu~
		END
		%IsBG2% %IsEETBG2% %IsTIWDonBG2% %IsTIWD2onBG2%
		BEGIN
			OUTER_SPRINT $UIMenuIF("Filespec") ~%PKGNAME%/menu/DeePermanentThieving/ActionBarButton3.menu~
		END
		%IsBGEE%
		BEGIN
			OUTER_SPRINT $UIMenuIF("Filespec") ~%PKGNAME%/menu/DeePermanentThieving/ActionBarButton4.menu~
		END
		%IsBG2onSoD%
		BEGIN
			OUTER_SPRINT $UIMenuIF("Filespec") ~%PKGNAME%/menu/DeePermanentThieving/ActionBarButton5.menu~
		END
		%IsLeUIBG2% %IsLeUISOD% %IsLeUIBG1% %IsEETLeUI%
		BEGIN
			OUTER_SPRINT $UIMenuIF("Filespec") ~%PKGNAME%/menu/DeePermanentThieving/ActionBarButton6.menu~
		END
	
		DEFAULT
			FAIL @15
	END
	LAUNCH_ACTION_MACRO UIMenuAppendMenuElements
	ACTION_IF NOT $UIMenuIF("Succeeded") THEN
	BEGIN
		FAIL @121
	END
	LAUNCH_ACTION_MACRO UIMenuUpdateMenu
	ACTION_IF NOT $UIMenuIF("Succeeded") THEN
	BEGIN
		FAIL @103
	END
			
	 // Patch location of existing Message Scrolls
	ACTION_IF (IsBG2onSoD) OR (MOD_IS_INSTALLED ~EEUITweaks.tp2~ ~2010~) THEN
	BEGIN
		ACTION_MATCH 1
		WITH
			%IsSoD% %IsEETSoD% %IsTIWDonSoD% %IsKRDS0% %IsKRDS1% %IsKRDS2% %IsKRDS3% %IsKRDS4% %IsKRDS5%
			BEGIN
				OUTER_SPRINT $UIMenuIF("Filespec") ~%PKGNAME%/menu/FeedbackHack/newbuttons1Large.menu~
			END
			%IsSoDOverhaul%
			BEGIN
				OUTER_SPRINT $UIMenuIF("Filespec") ~%PKGNAME%/menu/FeedbackHack/newbuttons2Large.menu~
			END
			%IsBG2% %IsEETBG2% %IsTIWDonBG2% %IsTIWD2onBG2%
			BEGIN
				OUTER_SPRINT $UIMenuIF("Filespec") ~%PKGNAME%/menu/FeedbackHack/newbuttons3Large.menu~
			END
			%IsBGEE%
			BEGIN
				OUTER_SPRINT $UIMenuIF("Filespec") ~%PKGNAME%/menu/FeedbackHack/newbuttons4Large.menu~
			END
			%IsBG2onSoD%
			BEGIN
				OUTER_SPRINT $UIMenuIF("Filespec") ~%PKGNAME%/menu/DeePermanentThieving/ABarScrBtns5.menu~
			END
			%IsLeUIBG2% %IsLeUISOD% %IsLeUIBG1% %IsEETLeUI%
			BEGIN
				OUTER_SPRINT $UIMenuIF("Filespec") ~%PKGNAME%/menu/FeedbackHack/newbuttons6Large.menu~
			END
	
			DEFAULT
				FAIL @15
		END
	
		LAUNCH_ACTION_MACRO DeesPermanent.ActionBarPatch2
		ACTION_IF NOT $UIMenuIF("Succeeded") THEN
		BEGIN
			FAIL @121
		END
		LAUNCH_ACTION_MACRO UIMenuUpdateMenu
		ACTION_IF NOT $UIMenuIF("Succeeded") THEN
		BEGIN
			FAIL @103
		END
	END
END // Not (IsKRDS)

// Close UI.MENU
LOG @13
SILENT
LAUNCH_ACTION_MACRO UIMenuClose
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @21
END

// Copy common files
LOG @24
SILENT
LAF EEUITweaks.CopyPVRZResource
	STR_VAR
		source_folder = EVAL ~%PKGNAME%/copy/DeePermanentThieving~
		resource_file = ~DIMSTEAL.BAM~
		pvrz_files = ~MOS5058.PVRZ~
	RET
		$UIMenuIF("Succeeded") = succeeded
END
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @44
END

// Copy configuration specific ACTION_BAR resources
ACTION_MATCH 1
WITH
	%IsSoD% %IsEETSoD% %IsTIWDonSoD% %IsKRDS0% %IsKRDS1% %IsKRDS2% %IsKRDS3% %IsKRDS4% %IsKRDS5%
	BEGIN
		ACTION_IF MOD_IS_INSTALLED ~EEUITweaks.tp2~ ~2010~ THEN
		BEGIN
			LAF EEUITweaks.CopyPVRZResource
				STR_VAR
					source_folder = EVAL ~%PKGNAME%/copy/CommonActionBars/SOD~
					resource_file = ~ABSODL.MOS~
					resource_rename = ~GUWBTP10.MOS~
					pvrz_files = ~MOS5042.PVRZ~
				RET
					$UIMenuIF("Succeeded") = succeeded
			END
			ACTION_IF NOT $UIMenuIF("Succeeded") THEN
			BEGIN
				FAIL @44
			END
		END ELSE
		BEGIN
			LAF EEUITweaks.CopyPVRZResource
				STR_VAR
					source_folder = EVAL ~%PKGNAME%/copy/CommonActionBars/SOD~
					resource_file = ~ABSODM.MOS~
					resource_rename = ~GUWBTP10.MOS~
					pvrz_files = ~MOS5041.PVRZ~
				RET
					$UIMenuIF("Succeeded") = succeeded
			END
			ACTION_IF NOT $UIMenuIF("Succeeded") THEN
			BEGIN
				FAIL @44
			END
		END
	END
	%IsSoDOverhaul%
	BEGIN
		ACTION_IF MOD_IS_INSTALLED ~EEUITweaks.tp2~ ~2010~ THEN
		BEGIN
			LAF EEUITweaks.CopyPVRZResource
				STR_VAR
					source_folder = EVAL ~%PKGNAME%/copy/CommonActionBars/DSP~
					resource_file = ~ABDSPL.MOS~
					resource_rename = ~GUWBTP10.MOS~
					pvrz_files = ~MOS5045.PVRZ~
				RET
					$UIMenuIF("Succeeded") = succeeded
			END
			ACTION_IF NOT $UIMenuIF("Succeeded") THEN
			BEGIN
				FAIL @44
			END
		END ELSE
		BEGIN
			LAF EEUITweaks.CopyPVRZResource
				STR_VAR
					source_folder = EVAL ~%PKGNAME%/copy/CommonActionBars/DSP~
					resource_file = ~ABDSPM.MOS~
					resource_rename = ~GUWBTP10.MOS~
					pvrz_files = ~MOS5044.PVRZ~
				RET
					$UIMenuIF("Succeeded") = succeeded
			END
			ACTION_IF NOT $UIMenuIF("Succeeded") THEN
			BEGIN
				FAIL @44
			END
		END
		DELETE ~override/GUWBTP10.png~
	END
	%IsBG2% %IsEETBG2% %IsTIWDonBG2% %IsTIWD2onBG2%
	BEGIN
		ACTION_IF MOD_IS_INSTALLED ~EEUITweaks.tp2~ ~2010~ THEN
		BEGIN
			LAF EEUITweaks.CopyPVRZResource
				STR_VAR
					source_folder = EVAL ~%PKGNAME%/copy/CommonActionBars/BG2~
					resource_file = ~ABBG2L.MOS~
					resource_rename = ~GUWBTP10.MOS~
					pvrz_files = ~MOS5039.PVRZ~
				RET
					$UIMenuIF("Succeeded") = succeeded
			END
			ACTION_IF NOT $UIMenuIF("Succeeded") THEN
			BEGIN
				FAIL @44
			END
		END ELSE
		BEGIN
			LAF EEUITweaks.CopyPVRZResource
				STR_VAR
					source_folder = EVAL ~%PKGNAME%/copy/CommonActionBars/BG2~
					resource_file = ~ABBG2M.MOS~
					resource_rename = ~GUWBTP10.MOS~
					pvrz_files = ~MOS5037.PVRZ~
				RET
					$UIMenuIF("Succeeded") = succeeded
			END
			ACTION_IF NOT $UIMenuIF("Succeeded") THEN
			BEGIN
				FAIL @44
			END
		END
	END
	%IsBGEE%
	BEGIN
		ACTION_IF MOD_IS_INSTALLED ~EEUITweaks.tp2~ ~2010~ THEN
		BEGIN
			LAF EEUITweaks.CopyPVRZResource
				STR_VAR
					source_folder = EVAL ~%PKGNAME%/copy/CommonActionBars/BG1~
					resource_file = ~ABBG1L.MOS~
					resource_rename = ~GUWBTP10.MOS~
					pvrz_files = ~MOS5048.PVRZ~
				RET
					$UIMenuIF("Succeeded") = succeeded
			END
			ACTION_IF NOT $UIMenuIF("Succeeded") THEN
			BEGIN
				FAIL @44
			END
		END ELSE
		BEGIN
			LAF EEUITweaks.CopyPVRZResource
				STR_VAR
					source_folder = EVAL ~%PKGNAME%/copy/CommonActionBars/BG1~
					resource_file = ~ABBG1M.MOS~
					resource_rename = ~GUWBTP10.MOS~
					pvrz_files = ~MOS5047.PVRZ~
				RET
					$UIMenuIF("Succeeded") = succeeded
			END
			ACTION_IF NOT $UIMenuIF("Succeeded") THEN
			BEGIN
				FAIL @44
			END
		END
	END
	%IsBG2onSoD%
	BEGIN
		LAF EEUITweaks.CopyPVRZResource
			STR_VAR
				source_folder = EVAL ~%PKGNAME%/copy/CommonActionBars/2SD~
				resource_file = ~AB2SDM.MOS~
				resource_rename = ~GUWBTP10.MOS~
				pvrz_files = ~MOS5049.PVRZ~
			RET
				$UIMenuIF("Succeeded") = succeeded
		END
		ACTION_IF NOT $UIMenuIF("Succeeded") THEN
		BEGIN
			FAIL @44
		END
	END
	%IsLeUIBG2% %IsLeUISOD% %IsLeUIBG1% %IsEETLeUI%
	BEGIN
		ACTION_IF MOD_IS_INSTALLED ~EEUITweaks.tp2~ ~2010~ THEN
		BEGIN
			LAF EEUITweaks.CopyPVRZResource
				STR_VAR
					source_folder = EVAL ~%PKGNAME%/copy/CommonActionBars/LUI~
					resource_file = ~ABLUIL.MOS~
					resource_rename = ~GUIBAR.MOS~
					pvrz_files = ~MOS5052.PVRZ~
				RET
					$UIMenuIF("Succeeded") = succeeded
			END
			ACTION_IF NOT $UIMenuIF("Succeeded") THEN
			BEGIN
				FAIL @44
			END
		END ELSE
		BEGIN
			LAF EEUITweaks.CopyPVRZResource
				STR_VAR
					source_folder = EVAL ~%PKGNAME%/copy/CommonActionBars/LUI~
					resource_file = ~ABLUIM.MOS~
					resource_rename = ~GUIBAR.MOS~
					pvrz_files = ~MOS5051.PVRZ~
				RET
					$UIMenuIF("Succeeded") = succeeded
			END
			ACTION_IF NOT $UIMenuIF("Succeeded") THEN
			BEGIN
				FAIL @44
			END
		END
	END
	%IsKRDS%
	BEGIN
	END

	DEFAULT
		FAIL @15
END

