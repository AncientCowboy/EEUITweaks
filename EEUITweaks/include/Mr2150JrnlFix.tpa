/*******************************************************************************
 * TextflashPatch
 *
 * Replaces 'e:GetActiveEngine():OnLeftPanelButtonClick(2)' button with
 * contents of file 'textflashbutton'
 * 
 * Results in $UIMenuIF("Succeeded") 1 => Success, 0 => Failure
 *
 ******************************************************************************/
DEFINE_ACTION_MACRO Mr2150JrnlFix.TextflashPatch
BEGIN
	LOCAL_SPRINT tempstr $UIMenuIF("Body")
	LOCAL_SPRINT adder ~~

	// Read patch insertion from textflashbutton.menu
	LOCAL_SET found = SIZE_OF_FILE ~%PKGNAME%/menu/Mr2150JrnlFix/textflashbutton.menu~
	COPY - ~%PKGNAME%/menu/Mr2150JrnlFix/textflashbutton.menu~ ~.../CounterInLine~
		READ_ASCII 0 adder (%found%)
	OUTER_SET found = 0 

	// Put current menu definition into inline file
	COPY - ~ToInlineTemplate~ ~.../CounterInLine~
		REPLACE_EVALUATE ~dummy~ BEGIN END
		~%tempstr%~
	OUTER_SPRINT tempstr ~~

	// Find the target button and replace it
	COPY - ~.../CounterInLine~ ~.../CounterInLine~
		REPLACE_EVALUATE CASE_INSENSITIVE ~^[%TAB% ]*button%MWHITE%{%MNOBRACE%+action%MWHITE%%MQUOTE%%MNOTQUOTE%+e:GetActiveEngine():OnLeftPanelButtonClick(2)%MNOTQUOTE%+%MQUOTE%%MNOBRACE%*}.*%MEOL%~
		BEGIN
			SET found = 1
		END
		~%adder%~

	// If not found, nothing to do but fail.
	ACTION_IF NOT %found% THEN
	BEGIN
		LOG ~In Mr2150JrnlFix.TextflashPatch - failed to find target button~
		OUTER_SET $UIMenuIF("Succeeded") = 0
	END

	// Success - copy .../CounterInLine to $UIMenuIF("Body")
	ACTION_IF %found% THEN
	BEGIN
		OUTER_SPRINT $UIMenuIF("Body") ~~
		OUTER_SET found = SIZE_OF_FILE ~.../CounterInLine~
		COPY - ~.../CounterInLine~ ~.../CounterInLine~
			READ_ASCII 0 $UIMenuIF("Body") (%found%-1)
			OUTER_SET $UIMenuIF("Succeeded") = 1
	END
	COPY - ~EmptyInlineTemplate~ ~.../CounterInline~
END


/*******************************************************************************
 * Mr2150's Journal Fixes Installation
 ******************************************************************************/
// Uninstall lefreut's Simple Journal Fixes if it has been installed
ACTION_IF MOD_IS_INSTALLED ~EEUITweaks.tp2~ ~3070~ THEN
BEGIN
	PRINT @302005
	PRINT @302006
	SILENT
	UNINSTALL ~EEUITweaks.tp2~ ~3070~
END

// Open UI.MENU
LOG @12
SILENT
LAUNCH_ACTION_MACRO UIMenuOpen
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @20
END

// Patch textflash menu
LOG @143
SILENT

OUTER_SPRINT $UIMenuIF("MenuName") ~textflash~
OUTER_SPRINT $UIMenuIF("Body") ~~
LAUNCH_ACTION_MACRO UIMenuGetMenu
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @144
END

LAUNCH_ACTION_MACRO Mr2150JrnlFix.TextflashPatch
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @145
END
LAUNCH_ACTION_MACRO UIMenuUpdateMenu
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @103
END

// Replace Journal Lua functions with ~~
LOG @302001
SILENT

OUTER_SPRINT $UIMenuIF("LuaRegex") ~function[%TAB% ]+initQuests[%TAB% ]*([%TAB% ]*)~
LAUNCH_ACTION_MACRO UIMenuGetMatchingLuaBlock
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @302002
END

OUTER_SPRINT $UIMenuIF("Body") ~~
LAUNCH_ACTION_MACRO UIMenuUpdateLuaBlock
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @302003
END

// Patch JOURNAL menu
LOG @146
SILENT

OUTER_SPRINT $UIMenuIF("MenuName") ~JOURNAL~
ACTION_MATCH 1
WITH
	%IsBG2%
	BEGIN
		LOG @28
		SILENT
		OUTER_SPRINT $UIMenuIF("Filespec") ~%PKGNAME%/menu/Mr2150JrnlFix/journalmenubg2.menu~
	END

	%IsEETBG2%
	BEGIN
		LOG @31
		SILENT
		OUTER_SPRINT $UIMenuIF("Filespec") ~%PKGNAME%/menu/Mr2150JrnlFix/journalmenubg2.menu~
	END

	%IsBG2onSoD%
	BEGIN
		LOG @16
		SILENT
		OUTER_SPRINT $UIMenuIF("Filespec") ~%PKGNAME%/menu/Mr2150JrnlFix/journalmenubg2.menu~
	END

	%IsBGEE%
	BEGIN
		LOG @14
		SILENT
		OUTER_SPRINT $UIMenuIF("Filespec") ~%PKGNAME%/menu/Mr2150JrnlFix/journalmenuee.menu~
	END

	%IsSoD%
	BEGIN
		LOG @19
		SILENT
		OUTER_SPRINT $UIMenuIF("Filespec") ~%PKGNAME%/menu/Mr2150JrnlFix/journalmenusod.menu~
	END

	%IsEETSoD%
	BEGIN
		LOG @32
		SILENT
		OUTER_SPRINT $UIMenuIF("Filespec") ~%PKGNAME%/menu/Mr2150JrnlFix/journalmenusod.menu~
	END

	%IsKRDS%
	BEGIN
		LOG @38
		SILENT
		OUTER_SPRINT $UIMenuIF("Filespec") ~%PKGNAME%/menu/Mr2150JrnlFix/journalmenusod.menu~
	END

	%IsTIWDonBG2% %IsTIWDonSoD% %IsTIWD2onBG2%
	BEGIN
		LOG @40
		SILENT
		OUTER_SPRINT $UIMenuIF("Filespec") ~%PKGNAME%/menu/Mr2150JrnlFix/journalmenubg2.menu~
	END

	DEFAULT
		FAIL @15
END

LAUNCH_ACTION_MACRO UIMenuReplaceMenuMixed
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @148
END

// Close up
LOG @13
SILENT
LAUNCH_ACTION_MACRO UIMenuClose
ACTION_IF NOT $UIMenuIF("Succeeded") THEN
BEGIN
	FAIL @21
END

// Copy files
LOG @24
SILENT
COPY ~%PKGNAME%/copy/Mr2150JrnlFix/M_JFlang.lua~ ~override~
