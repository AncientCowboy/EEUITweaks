
/*******************************************************************************
 * EEUIGetMenuEnv
 * Sets global variables representing current UI.MENU configuration. Currently
 * defined variables are:
 * 
 * IsBG2 - BG2EE generic.
 * IsBGEE - Base BGEE, no SOD
 * IsSoD  - Base BGEESoD
 * IsBG2onSoD - Kerozevok's BG2EE To BGEE Mod
 * IsSoDOverhaul - Pecca's Dragonspear UI++
 * IsEETBG2  - EET with default BG2 GUI
 * IsEETSoD  - EET with SoD GUI Option
 * IsLeUIBG2 - Lefreut's UI on BG2EE
 * IsLeUISOD - Lefreut's UI on BGEESoD
 * IsLeUIBG1 - Lefreut's UI on BGEE no SoD
 * IsEETLeUI - EET with Lefreut's UI
 * IsKRDS - Kilivitz's Revised Dragon Scale UI on SoD, prior to V3
 * IsKRDS0 - Revised Dragon Scale V4, component 0 General tweaks, Maps, Start and Escape Menus
 * IsKRDS1 - Inventory
 * IsKRDS2 - Record
 * IsKRDS3 - Classic Spellbook
 * IsKRDS4 - Larger Ring Icons
 * IsKRDS5 - Alternate Fonts
 * IsTIWDonBG2 - trinit's IWD GUI on BG2
 * IsTIWDonSoD - trinit's IWD GUI on SoD
 * IsTIWD2onBG2 - trinit's IWD2 GUI on BG2EE
 * IsPSTEE - Planescape Torment EE
 * IsPSTEE - Icewind Dale EE
 ******************************************************************************/
DEFINE_ACTION_MACRO EEUIGetMenuEnv
BEGIN
	OUTER_SET IsBG2 = 0
	OUTER_SET IsBGEE = 0
	OUTER_SET IsSoD = 0
	OUTER_SET IsBG2onSoD = 0
	OUTER_SET IsSoDOverhaul = 0
	OUTER_SET IsEETBG2 = 0
	OUTER_SET IsEETSoD = 0
	OUTER_SET IsLeUIBG2 = 0
	OUTER_SET IsLeUISOD = 0
	OUTER_SET IsLeUIBG1 = 0
	OUTER_SET IsEETLeUI = 0
	OUTER_SET IsKRDS = 0
	OUTER_SET IsKRDS0 = 0
	OUTER_SET IsKRDS1 = 0
	OUTER_SET IsKRDS2 = 0
	OUTER_SET IsKRDS3 = 0
	OUTER_SET IsKRDS4 = 0
	OUTER_SET IsKRDS5 = 0
	OUTER_SET IsTIWDonBG2 = 0
	OUTER_SET IsTIWDonSoD = 0
	OUTER_SET IsTIWD2onBG2 = 0
	OUTER_SET IsPSTEE = 0
	OUTER_SET IsIWDEE = 0

	ACTION_IF GAME_IS ~iwdee~ THEN
	BEGIN
		ACTION_IF (FILE_EXISTS_IN_GAME ~UI.MENU~) THEN
		BEGIN
			OUTER_SET IsIWDEE = 1
			LOG ~IsIWDEE~
		END
	END

	ACTION_IF GAME_IS ~pstee~ THEN
	BEGIN
		ACTION_IF (FILE_EXISTS_IN_GAME ~UI.MENU~) THEN
		BEGIN
			OUTER_SET IsPSTEE = 1
			LOG ~IsPSTEE~
		END
	END

	ACTION_IF GAME_IS ~eet~ THEN
	BEGIN
		ACTION_IF MOD_IS_INSTALLED ~setup-EET_gui.tp2~ ~0~ THEN
		BEGIN
			OUTER_SET IsEETSoD = 1
			LOG ~IsEETSoD~
			ACTION_IF FILE_EXISTS ~override/M_CKUI.LUA~ THEN
			BEGIN
				ACTION_IF (MOD_IS_INSTALLED ~CKUISOD.tp2~ ~0~)
				BEGIN
					LOG ~IsKRDS0~
					OUTER_SET IsKRDS0 = 1
				END
				ACTION_IF (MOD_IS_INSTALLED ~CKUISOD.tp2~ ~1~)
				BEGIN
					LOG ~IsKRDS1~
					OUTER_SET IsKRDS1 = 1
				END
				ACTION_IF (MOD_IS_INSTALLED ~CKUISOD.tp2~ ~2~)
				BEGIN
					LOG ~IsKRDS2~
					OUTER_SET IsKRDS2 = 1
				END
				ACTION_IF (MOD_IS_INSTALLED ~CKUISOD.tp2~ ~3~)
				BEGIN
					LOG ~IsKRDS3~
					OUTER_SET IsKRDS3 = 1
				END
				ACTION_IF (MOD_IS_INSTALLED ~CKUISOD.tp2~ ~4~)
				BEGIN
					LOG ~IsKRDS4~
					OUTER_SET IsKRDS4 = 1
				END
				ACTION_IF (MOD_IS_INSTALLED ~CKUISOD.tp2~ ~5~)
				BEGIN
					LOG ~IsKRDS5~
					OUTER_SET IsKRDS5 = 1
				END
			END
		END ELSE
		BEGIN
			ACTION_IF MOD_IS_INSTALLED ~LeUI.tp2~ ~0~ THEN
			BEGIN
				OUTER_SET IsEETLeUI = 1
				LOG ~IsEETLeUI~
			END ELSE BEGIN
				OUTER_SET IsEETBG2 = 1
				LOG ~IsEETBG2~
			END
		END
	END

	ACTION_IF GAME_IS ~bg2ee~ THEN
	BEGIN
		ACTION_IF MOD_IS_INSTALLED ~LeUI.tp2~ ~0~ THEN
		BEGIN
			OUTER_SET IsLeUIBG2 = 1
			LOG ~IsLeUIBG2~
		END ELSE
		BEGIN
			ACTION_IF FILE_EXISTS ~override/guiweidufile.png~ THEN
			BEGIN
				OUTER_SET IsTIWDonBG2 = 1
				LOG ~IsTIWDonBG2~
			END ELSE
			BEGIN
				ACTION_IF FILE_EXISTS ~override/guiiwd2dummy.png~ THEN
				BEGIN
					OUTER_SET IsTIWD2onBG2 = 1
					LOG ~IsTIWD2onBG2~
				END ELSE
				BEGIN
					OUTER_SET IsBG2 = 1
					LOG ~IsBG2~
				END
			END
		END
	END

	ACTION_IF GAME_IS ~bgee~ THEN
	BEGIN
		ACTION_IF NOT FILE_EXISTS_IN_GAME ~BD0010.ARE~ THEN
		BEGIN // Non-SoD
			ACTION_IF MOD_IS_INSTALLED ~LeUI.tp2~ ~0~ THEN
			BEGIN
				OUTER_SET IsLeUIBG1 = 1
				LOG ~IsLeUIBG1~
			END ELSE
			BEGIN
				OUTER_SET IsBGEE = 1
				LOG ~IsBGEE~
			END
		END ELSE
		BEGIN // SoD
			ACTION_IF MOD_IS_INSTALLED ~dragonspear_ui++.tp2~ ~1~ THEN
			BEGIN
				OUTER_SET IsSoDOverhaul = 1
				LOG ~IsSoDOverhaul~
			END ELSE
			BEGIN
				ACTION_IF MOD_IS_INSTALLED ~LeUI.tp2~ ~0~ THEN
				BEGIN
					OUTER_SET IsLeUISOD = 1
					LOG ~IsLeUISOD~
				END ELSE
				BEGIN
					ACTION_IF FILE_EXISTS ~override/ULGOTHST.MOS~ THEN
					BEGIN
						OUTER_SET IsBG2onSoD = 1
						LOG ~IsBG2onSoD~
					END ELSE
					BEGIN
						ACTION_IF FILE_EXISTS ~override/M_CKUI.LUA~ THEN
						BEGIN
							OUTER_SET IsKRDS = 1
							LOG ~IsKRDS~
							ACTION_IF (MOD_IS_INSTALLED ~CKUISOD.tp2~ ~0~)
							BEGIN
								LOG ~IsKRDS0 not IsKRDS~
								OUTER_SET IsKRDS0 = 1
								OUTER_SET IsKRDS = 0
							END
							ACTION_IF (MOD_IS_INSTALLED ~CKUISOD.tp2~ ~1~)
							BEGIN
								LOG ~IsKRDS1 not IsKRDS~
								OUTER_SET IsKRDS1 = 1
								OUTER_SET IsKRDS = 0
							END
							ACTION_IF (MOD_IS_INSTALLED ~CKUISOD.tp2~ ~2~)
							BEGIN
								LOG ~IsKRDS2 not IsKRDS~
								OUTER_SET IsKRDS2 = 1
								OUTER_SET IsKRDS = 0
							END
							ACTION_IF (MOD_IS_INSTALLED ~CKUISOD.tp2~ ~3~)
							BEGIN
								LOG ~IsKRDS3 not IsKRDS~
								OUTER_SET IsKRDS3 = 1
								OUTER_SET IsKRDS = 0
							END
							ACTION_IF (MOD_IS_INSTALLED ~CKUISOD.tp2~ ~4~)
							BEGIN
								LOG ~IsKRDS4 not IsKRDS~
								OUTER_SET IsKRDS4 = 1
								OUTER_SET IsKRDS = 0
							END
							ACTION_IF (MOD_IS_INSTALLED ~CKUISOD.tp2~ ~5~)
							BEGIN
								LOG ~IsKRDS5 not IsKRDS~
								OUTER_SET IsKRDS5 = 1
								OUTER_SET IsKRDS = 0
							END
						END ELSE
						BEGIN
							ACTION_IF FILE_EXISTS ~override/guiweidufile.png~ THEN
							BEGIN
								OUTER_SET IsTIWDonSoD = 1
								LOG ~IsTIWDonSoD~
							END ELSE
							BEGIN
								OUTER_SET IsSoD = 1
								LOG ~IsSoD~
							END
						END
					END
				END
			END
		END
	END
END
